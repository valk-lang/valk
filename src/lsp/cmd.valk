
#if OS == win
header "win" as win
#end

use valk:io

fn help(code: i32) $exit {

    print("\n")
    println("# Start the LSP server");
    println("> valk lsp run");
    print("\n")

    exit(code)
} 

fn run(args: Array[String]) {
    let show_help = args.contains("-h") || args.contains("--help")
    if show_help : help(0)
    let action = args.pop_first() ! help(1)
    if action == "run" {
        start_server()
    } else {
        help(1)
    }
}

fn start_server() {
    let buf = ByteBuffer.new(1024)
    let l = Lsp{ buf: buf }

    #if OS == win
    // Stop converting \n to \r\n otherwise \r\n becomes \r\r\n
    win:_setmode(0, win:O_BINARY)
    win:_setmode(1, win:O_BINARY)
    #end

    l.log("LSP server is running")
    while true {

        buf.minimum_free_space(1024)
        let bytes = io:read_to_ptr_sync(0, buf.data.@offset(buf.length), 1024, buf.length) ! {
            l.log("No more input. Stopping LSP server")
            exit(1)
        }
        buf.length += bytes

        if buf.length == 0 {
            l.log("No more input (EOF). Stopping LSP server")
            exit(1)
        }

        l.parse()
    }
}
