
fn ir_ast(ir: IR, scope: Scope) {
    let tokens = scope.ast
    ir_tokens(ir, tokens)
}

fn ir_tokens(ir: IR, tokens: Array[Token]) {

    each tokens as token {

        let tt = token.type

        if tt == AST.statement {
            let irv = ir_value(ir, token.get_value1())
            continue
        }
        if tt == AST.declare {
            let decl = token.get_decl()
            let val = token.get_value1()
            let irv = ir_value(ir, val)
            let func = ir.func().func
            if !decl.is_used : continue
            if decl.is_mut || (decl.is_gc && !func.disable_gc_stack) {
                ir_store(ir, decl.ir_name, irv, ir_type(decl.type))
            }
            if !decl.is_mut {
                decl.ir_imut_value = irv
            }
            continue
        }
        if tt == AST.return {
            let val = token.value1
            if !isset(val) {
                ir_ret_void(ir)
                continue
            }
            ir_ret(ir, ir_value(ir, val), ir_type(val.rett))
            continue
        }
        if tt == AST.vscope_return {
            let block = ir.block_vscope_after
            if !isset(block) : ir.error("Missing IR vscope block (bug)")
            ir_jump(ir, block)
            continue
        }
        if tt == AST.while {
            let cond = token.value1.@cast(Value)
            let scope = token.scope1.@cast(Scope)
            ir_ast_while(ir, cond, scope)
            continue
        }
        if tt == AST.each {
            let sub = token.scope1.@cast(Scope)
            ir_ast_each(ir, sub)
            continue
        }
        if tt == AST.if {
            let cond = token.value1.@cast(Value)
            let scope = token.scope1.@cast(Scope)
            let scope_else = token.scope2
            ir_ast_if(ir, cond, scope, scope_else)
            continue
        }
        if tt == AST.assign {
            let left = token.value1.@cast(Value)
            let right = token.value2.@cast(Value)
            let on = left.value1.@cast(Value)

            let ir_right = ir_value(ir, right)
            if (left.type == VAL.prop && on.rett.is_gc() && left.rett.is_gc()) {
                // Gc link
                let prop = left.prop.@cast(Prop)
                let ir_on = ir_value(ir, on);
                let ir_left = ir_value_assign(ir, left)
                let res = ir_gc_update_prop(ir, ir_on, ir_left, ir_right)
            } else {
                let ir_left = ir_value_assign(ir, left)
                // ir_store(ir, ir_left, ir_right, ir_type(left.rett))
                ir_store(ir, ir_left, ir_right, ir_type(right.rett))
            }
            continue
        }
        if tt == AST.break {
            let b = ir.block_loop_after
            if !isset(b) : ir.error("Missing ir block for 'break' (bug)")
            ir_jump(ir, b)
            continue
        }
        if tt == AST.continue {
            let b = ir.block_loop_cond
            if !isset(b) : ir.error("Missing ir block for 'break' (bug)")
            ir_jump(ir, b)
            continue
        }
        if tt == AST.ir_comment {
            ir_com(ir, token.string1)
            continue
        }
        if tt == AST.inline_scope {
            let sub = token.scope1.@cast(Scope)
            ir_ast(ir, sub)
            continue
        }
        if tt == AST.decl_offset {
            let decl = token.decl ?! panic("IR bug: Missing decl while defining decl_offset")
            let irfunc = ir.func()
            let func = irfunc.func
            let type = ir_type(decl.type)

            let code = ir.code()
            let start = irfunc.block_start.code

            if decl.is_gc && !func.disable_gc_stack {
                //
                let stack_adr = func.fast().cache_stack_adr ?! panic("IR bug: Missing cache_stack_adr in decl_offset")
                let ir_stack_adr = ir_value(ir, stack_adr)
                //
                code.append_str("  ")
                code.append_str(decl.ir_name)
                code.append_str(" = getelementptr i8, ptr ")
                code.append_str(ir_stack_adr)
                code.append_str(", i32 ")
                code.append_str(decl.offset)
                code.append_str("\n")
            } else {
                code.append_str("  ")
                code.append_str(decl.ir_name)
                code.append_str(" = alloca ")
                code.append_str(type)
                code.append_str(", align ")
                code.append_uint(decl.type.align_size(false))
                code.append_str("\n")
            }

            if decl.is_arg {
                code.append_str("  store ")
                code.append_str(type)
                code.append_str(" ")
                if decl.is_mut {
                    code.append_str(decl.ir_name)
                    code.append_str("_init, ptr ")
                } else {
                    code.append_str(decl.ir_name)
                    code.append_str(", ptr ")
                }
                code.append_str(decl.ir_name)
                code.append_str(", align ")
                code.append_uint(decl.type.align_size(false))
                code.append_str("\n")
            } else if decl.is_gc && !func.disable_gc_stack {
                code.append_str("  store ptr null, ptr ")
                code.append_str(decl.ir_name)
                code.append_str(", align ")
                code.append_uint(decl.type.align_size(false))
                code.append_str("\n")
            }
            continue
        }

        ir.error("Unknown IR token: " + tt)
    }
}

fn ir_ast_while(ir: IR, cond: Value, scope: Scope) {

    let func = ir.func()
    let b_cond = func.new_block("while_cond")
    let b_code = func.new_block("while_code")
    let b_after = func.new_block("while_after")

    ir_jump(ir, b_cond)

    ir.set_block(b_cond)
    let ir_cond = ir_value(ir, cond)
    ir_cond_jump(ir, ir_cond, b_code, b_after)

    // Code
    ir.set_block(b_code)

    // Ast
    let backup_cond = ir.block_loop_cond
    let backup_after = ir.block_loop_after
    ir.block_loop_cond = b_cond
    ir.block_loop_after = b_after
    ir_ast(ir, scope)
    ir.block_loop_cond = backup_cond
    ir.block_loop_after = backup_after

    if !scope.did_return : ir_jump(ir, b_cond)
    //
    ir.set_block(b_after)
}

fn ir_ast_each(ir: IR, scope: Scope) {

    let func = ir.func()
    let b_code = func.new_block("each_code")
    let b_after = func.new_block("each_after")
    ir_jump(ir, b_code)

    ir.set_block(b_code)
    let backup_cond = ir.block_loop_cond
    let backup_after = ir.block_loop_after
    ir.block_loop_cond = b_code
    ir.block_loop_after = b_after
    ir_ast(ir, scope)
    ir.block_loop_cond = backup_cond
    ir.block_loop_after = backup_after

    if !scope.did_return : ir_jump(ir, b_code)

    ir.set_block(b_after)
}

fn ir_ast_if(ir: IR, cond: Value, scope: Scope, scope_else: ?Scope) {

    let func = ir.func()
    let b_code = func.new_block("if_code")
    let b_else : ?IRBlock = isset(scope_else) ? func.new_block("if_else") : null.@cast(?IRBlock)
    let b_after = func.new_block("if_after")

    let ir_cond = ir_value(ir, cond)
    ir_cond_jump(ir, ir_cond, b_code, b_else ?? b_after)

    // Code
    ir.set_block(b_code)
    ir_ast(ir, scope)
    if !scope.did_return : ir_jump(ir, b_after)

    // Else
    if isset(b_else) && isset(scope_else) {
        ir.set_block(b_else)
        ir_ast(ir, scope_else)
        if !scope_else.did_return : ir_jump(ir, b_after)
    }

    ir.set_block(b_after)
}
