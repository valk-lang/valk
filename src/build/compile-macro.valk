

fn parse_compile_macro(p: Parser) {
    let t = p.tok(false, false)

    if t == tok_word {
        if p.word_is("if") {
            let result = parse_compile_macro_condition(p)
            p.cm_results.push(result)
            if result == 0 : parse_skip_compile_macro_block(p)
            return
        }
        if p.word_is("elif") {
            let index = p.cm_results.length - 1
            let prev = p.cm_results.get(index) ! p.error("Unexpected '#elif' token. There's no previous #if to match with")
            if prev == 0 {
                let result = parse_compile_macro_condition(p)
                p.cm_results.set(index, result) ! p.error("Failed to set compiler macro #elif value (compiler bug)")
                if result == 0 : parse_skip_compile_macro_block(p)
            } else {
                parse_skip_compile_macro_block(p)
            }
            return
        }
        if p.word_is("else") {
            let index = p.cm_results.length - 1
            let prev = p.cm_results.get(index) ! p.error("Unexpected '#else' token. There's no previous #if to match with")
            if prev == 1 {
                parse_skip_compile_macro_block(p)
            }
            return
        }
        if p.word_is("end") {
            let res = p.cm_results.pop() ! p.error("Unexpected '#end' token. There's no previous #if to match with")
            return
        }
    }

    p.error_token()
}

fn parse_skip_compile_macro_block(p: Parser) {
    let depth = 1
    while true {
        let before_i = p.i
        let t = p.tok(true, true)
        if t == tok_end : p.error("Missing compile macro '#end' token")
        if p.on_newline && p.sign_is("#") {

            t = p.tok(false, false)

            if p.word_is("if") : depth++
            else if p.word_is("end") : depth--
            else if depth == 1 && (p.word_is("else") || p.word_is("elif")) : depth--

            if depth == 0 {
                p.i = before_i
                break
            }
        }
    }
}

fn parse_skip_compile_macro(p: Parser) {
    let t = p.tok(false, false)

    if t == tok_word {
        if p.word_is("if") {
            p.cm_results.push(0)
            parse_skip_compile_macro_block(p)
            return
        }
        if p.word_is("elif") {
            p.cm_results.get(0) ! p.error("Unexpected '#elif' token. There's no previous #if to match with")
            parse_skip_compile_macro_block(p)
            return
        }
        if p.word_is("else") {
            p.cm_results.get(0) ! p.error("Unexpected '#else' token. There's no previous #if to match with")
            return
        }
        if p.word_is("end") {
            p.cm_results.pop() ! p.error("Unexpected '#end' token. There's no previous #if to match with")
            return
        }
    }
}

fn parse_compile_macro_condition(p: Parser) u8 {
    let t = p.tok(true, false)
    let defs = p.build.cm_defs
    let result: u8 = 2

    if t == tok_word {
        let word = p.word()
        let def_value = defs.get(word) ! p.error("Unknown compile macro definition: '" + word + "'")
        result = def_value == "0" ? 0 : 1

        t = p.tok(true, false, false)
        if t == tok_sign && (p.word_is("==") || p.word_is("!=")) {
            t = p.tok(true, false)
            let not_eq = p.word_is("!=")
            t = p.tok(true, false)
            if t == tok_word {
                let value = p.word()
                result = def_value == value ? 1 : 0
            } else if t == tok_int {
                let value = p.word()
                result = def_value == value ? 1 : 0
            } else {
                p.error("Invalid right side value: '" + p.word() + "'")
            }

            if not_eq {
                result = result == 1 ? 0 : 1
            }
        }
    } else if t == tok_sign {
        if p.word_is("!") {
            result = parse_compile_macro_condition(p)
            result == 1 ? 0 : 1
        }
    }

    if result == 2 {
        p.error_token()
    }

    return result
}

fn parse_compile_macro_scope_end_check(p: Parser) {
    if p.cm_results.length > 0 {
        p.error("Missing compile macro '#end' token")
    }
}
