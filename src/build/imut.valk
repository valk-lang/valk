
fn get_mut_type(type: Type) Type {
    if !type.is_imut : return type
    if !type.is_structural_type() : return type
    let class = type.class
    if !isset(class) : return type
    if !class.is_imut : return type

    let mclass = class.mut_class
    if !isset(mclass) : return type
    let t = type.clone()
    t.class = mclass
    t.is_imut = false
    return t
}

fn get_imut_type(type: Type) Type {
    if type.is_imut : return type
    if type.is_closure() {
        let t = type.clone()
        t.is_imut = false
        return t
    }
    if !type.is_structural_type() : return type
    let class = type.class
    if !isset(class) : return type
    if class.is_imut : return type

    let imut_class = get_imut_class(class)
    let t = type.clone()
    t.class = imut_class
    t.is_imut = true
    return t
}


fn get_imut_class(class: Class) Class {

    let _iclass = class.imut_class
    if isset(_iclass) : return _iclass

    let u = class.unit

    let iclass = u.new_class(class.fc, class.act, class.name, class.type, null, class.chunk)
    iclass.name = "imut_" + class.name
    iclass.export_name = "imut_" + class.export_name
    iclass.display_name = "imut " + class.display_name
    iclass.chunk = class.chunk
    iclass.mut_class = class
    iclass.is_imut = true

    iclass.is_generic_base = class.is_generic_base
    iclass.generic_names = class.generic_names
    iclass.generic_types = class.generic_types

    class.imut_class = iclass

    if iclass.is_generic_base {
        iclass.generics = Map[Class]{}

        return iclass
    }

    class_parse_props(iclass, iclass.generic_types)

    each iclass.funcs as name, func {
        parse_func_args(func)
    }

    if iclass.build.class_sizes_known {
        iclass.determine_size() ! u.build.error("Cannot determine class size of: " + iclass.get_type())
    }

    return iclass
}
