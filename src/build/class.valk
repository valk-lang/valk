
class Class {
    unit: Unit
    build: Build
    fc: Fc
    act: int
    name: String
    export_name: String
    display_name: String
    def_chunk: ?Chunk (null)
    chunk: ?Chunk
    type: int
    _type: ?Type (null)
    scope: Scope
    //
    size: uint (0)
    props: Map[Prop] (Map[Prop]{})
    funcs: Map[Func] (Map[Func]{})
    groups: Map[Group] (Map[Group]{})
    names: Array[String] (Array[String]{})
    extends: Array[Extend] (Array[Extend]{})
    // Hooks
    eq_hooks: Array[Func] (Array[Func]{})
    lt_hooks: Array[Func] (Array[Func]{})
    lte_hooks: Array[Func] (Array[Func]{})
    gt_hooks: Array[Func] (Array[Func]{})
    gte_hooks: Array[Func] (Array[Func]{})
    to_hooks: Array[Func] (Array[Func]{})
    add_hooks: Array[Func] (Array[Func]{})
    offset: ?Func (null)
    offset_assign: ?Func (null)
    //
    pool_index: uint (0)
    gc_prop_count: uint (0)
    vtable: Map[Func] (Map[Func]{})
    //
    generic_names: ?Map[GenericName] (null)
    generic_types: ?Array[Type] (null)
    generics: ?Map[Class] (null)
    generic_from: ?Class (null)
    imut_class: ?Class (null)
    mut_class: ?Class (null)
    // Mode
    mode_for_class: ?Class (null)
    mode_for_chunk: ?Chunk (null)
    // Macros
    macros: ?Map[Macro] (null)
    //
    is_signed: bool (false)
    size_known: bool (false)
    is_generic_base: bool (false)
    parsed_props: bool (false)
    packed: bool (false)
    uses_vtable: bool (false)
    is_imut: bool (false)
    is_async: bool (false)
    no_new_props: bool (false)
    is_mode: bool (false)
    gen_docs: bool (true)

    fn get_type() Type {
        let t = this._type
        if !isset(t) {
            // Create type
            let b = this.build
            let ct = this.type
            let tt = TYPE.class
            if ct == CT.class : tt = TYPE.class
            else if ct == CT.struct : tt = TYPE.struct
            else if ct == CT.int : tt = TYPE.int
            else if ct == CT.float : tt = TYPE.float
            else if ct == CT.pointer : tt = TYPE.voidptr
            else if ct == CT.bool : tt = TYPE.bool
            else : b.error("Unknown type in function Class.get_type() (compiler bug)")
            let res = Type {
                type: tt
                build: b
                class: this
                is_signed: this.is_signed
                is_imut: this.is_imut
            }
            if this.is_pointer() && this.type != CT.pointer {
                let n = Type.new(b, TYPE.ptr)
                n.sub_type = res
                res = n
            }
            this._type = res
            return res
        }
        return t
    }
    fn is_pointer() bool {
        let ct = this.type
        return ct == CT.class || ct == CT.struct || ct == CT.pointer
    }

    fn get_name(full: bool) String {
        let b = this.build
        let result = full ? this.export_name : this.display_name
        if !full {
            let fc = this.fc
            let nsc = fc.nsc
            let main = b.nsc_main()
            if nsc != main && nsc.pkc != b.pkc_valk() {
                result = nsc.name + ":" + result
                if nsc.pkc != main.pkc {
                    result = nsc.pkc.name + ":" + result
                }
            }
        }
        if this.is_imut {
            result = "imut " + result
        }
        return result
    }

    fn determine_size() !unable {
        if this.size > 0 {
            this.size_known = true
            return 
        }

        let b = this.build
        let props = this.props
        let props_new = Map[Prop]{}

        // Check if vtable needs to be added
        if this.funcs.has("gc_action") || this.funcs.has("gc_free") {
            this.uses_vtable = true

            let f1 = this.funcs.get("gc_action") !? null
            if isset(f1) : this.vtable.set("gc_action", f1)
            let f2 = this.funcs.get("gc_free") !? null
            if isset(f2) : this.vtable.set("gc_free", f2)
        }
        if this.uses_vtable {
            let name = "_VTABLE"
            let prop = Prop {
                name: name
                type: type_ptr(b)
                class: this
                act: act_default
            }
            if this.funcs.has(name) : b.error("Property name is already used for a function: " + name)
            props_new.set(name, prop)
        }

        // Resort properties
        let gc_prop_count : uint = 0
        each props as prop, name {
            if prop.type.is_gc() {
                props_new.set(name, prop)
                gc_prop_count++
            }
        }
        each props as prop, name {
            if !prop.type.is_gc() {
                props_new.set(name, prop)
            }
        }
        props = props_new
        this.props = props
        this.gc_prop_count = gc_prop_count

        // Calculate size
        let ptr_size = b.ptr_size
        let size : uint = 0
        each props as prop, name {
            let prop_size = prop.type.size()
            if prop_size == 0 : throw unable
            let mod = prop.type.largest_property_offset()

            if !this.packed {
                size += size % mod
            }
            size += prop_size
        }

        // Round off
        if size % ptr_size != 0 {
            size += ptr_size - (size % ptr_size)
        }

        if b.verbose > 2 : b.log("> Determine class size: " + this.display_name + " | size: " + size)

        this.size = size
        this.size_known = true

        // Determine pool index
        if size <= 16 * ptr_size {
            // let i : uint = size == 0 ? 0 : (size / ptr_size - 1)
            let i : uint = size / ptr_size - 1
            this.pool_index = i
        } else {
            let cmp = 16 * ptr_size * 2
            let i : uint = 16
            while size > cmp {
                cmp *= 2
                i++
            }
            this.pool_index = i
        }

        this.determine_prop_offsets(b)
    }

    fn determine_prop_offsets(b: Build) {
        let offset : uint = 0
        let props = this.props

        each props as prop, name {
            let ps = prop.type.size()
            let mod = prop.type.largest_property_offset()

            let skip : uint = this.packed ? 0 : (offset % mod)
            prop.offset = (offset + skip).@cast(int)
            offset += skip + ps
        }
    }

    fn is_structural() bool {
        return this.type == CT.struct || this.type == CT.class
    }

    fn vtable_export_name() String {
        return this.export_name + "_VTABLE"
    }

    fn validate_internals() {
        let b = this.build
        let offset = this.offset
        let offset_assign = this.offset_assign
        if isset(offset) {
            if offset.args.length == 0 : b.error("The $offset function '" + offset.display_name + "' must have atleast 1 argument")
            let retts = offset.rett_types
            if retts.length != 1 : b.error("The $offset function '" + offset.display_name + "' can only have 1 return type")
            let rett = retts.get(0) !? type_void(b)
            if rett.is_void() : b.error("The $offset function '" + offset.display_name + "' cannot have a 'void' return type")
        }
        if isset(offset_assign) {
            if offset_assign.args.length <= 1 : b.error("The $offset_assign function '" + offset_assign.display_name + "' must have atleast 2 argument")
        }
    }

    fn get_to_hook(type: Type) Func !not_found {
        let hooks = this.to_hooks
        each hooks as func {
            if type.compat(func.rett) && (type.get_class() !? null) == (func.rett.get_class() !? null) : return func
        }
        throw not_found
    }
    fn get_add_hook(type: Type) Func !not_found {
        let hooks = this.add_hooks
        each hooks as func {
            let arg = func.args.get(0) ! continue
            if arg.type.compat(type) : return func
        }
        let tclass = type.get_class() !? null
        if isset(tclass) {
            each hooks as func {
                let arg = func.args.get(0) ! continue
                tclass.get_to_hook(arg.type) ! continue
                return func
            }
        }
        throw not_found
    }

    fn get_prop(name: String) Prop !not_found {
        let prop = this.props.get(name) !? null
        if isset(prop) : return prop
        let for_class = this.mode_for_class
        if isset(for_class) {
            let func = this.funcs.get(name) !? null
            if !isset(func) {
                let prop = for_class.props.get(name) !? null
                if isset(prop) : return prop
            }
        }
        throw not_found
    }
    fn get_props() Map[Prop] {
        let for_class = this.mode_for_class
        if isset(for_class) : return for_class.get_props()
        return this.props
    }
    fn get_gc_prop_count() uint {
        let for_class = this.mode_for_class
        if isset(for_class) : return for_class.get_gc_prop_count()
        return this.gc_prop_count
    }

    fn get_func(name: String) Func !not_found {
        let prop = this.funcs.get(name) !? null
        if isset(prop) : return prop
        let for_class = this.mode_for_class
        if isset(for_class) {
            let prop = for_class.funcs.get(name) !? null
            if isset(prop) : return prop
        }
        throw not_found
    }
    fn get_funcs() Map[Func] {
        let funcs = Map[Func]{} 
        let for_class = this.mode_for_class
        if isset(for_class) {
            each for_class.funcs as func {
                funcs.set(func.name, func)
            }
        }
        each this.funcs as func {
            funcs.set(func.name, func)
        }
        return funcs
    }

    fn get_group(name: String) Group !not_found {
        let prop = this.groups.get(name) !? null
        if isset(prop) : return prop
        let for_class = this.mode_for_class
        if isset(for_class) {
            let prop = for_class.groups.get(name) !? null
            if isset(prop) : return prop
        }
        throw not_found
    }
}

class Prop {
    name: String
    type: Type
    class: Class
    act: int
    offset: int (0)
    chunk: ?Chunk (null)
    def_chunk: ?Chunk (null)
    is_lazy: bool (false)
}

fn prop_read_default(p: Parser, scope: Scope, prop: Prop) Value {
    let chunk = prop.chunk
    if !isset(chunk) {
        if prop.type.nullable {
            return vgen_null(prop.type)
        }
        p.error("Missing value for property: " + prop.name)
    }
    // Scope
    let sub = scope.sub_scope(SCOPE.default)
    sub.parent_idf = prop.class.scope
    //
    let sp = p.new_sub_parser(chunk)
    sp.set_suggest(prop.type)
    let v = read_value(sp, sub)
    if v.type != VAL.undefined {
        v = v.try_convert(sp.ctx, sub, prop.type)
        prop.type.compat_check(v.rett, sp)
    }
    sp.pop_suggest()
    //
    return v
}
