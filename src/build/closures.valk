

fn func_ref_value_to_closure(v: Value, decl_scope: Scope) Value {
    return closure_init_value(v, null, decl_scope)
}

fn closure_init_value(fptr: Value, bind_values: ?Array[Value], decl_scope: Scope) Value {

    let b = fptr.rett.build
    let fi = fptr.rett.func_info
    if !isset(fi) : b.error("Missing closure function info (bug)")

    let parent_func = decl_scope.func
    if !isset(parent_func) : b.error("Missing closure parent function (bug)")

    // Init data class instance
    let data : Value = vgen_null(b.valk_type("type", "ptr"))
    let data_class : ?Class = null
    if isset(bind_values) {
        if bind_values.length > 0 {
            data_class = closure_gen_data_class(b, parent_func.unit, bind_values)
            let values_data = Map[Value].new()
            each bind_values as i, bv {
                values_data.set("data_" + i, bv)
            }
            data = vgen_class_init(data_class @as Class, decl_scope, values_data)
        }
    }

    // Generate closure type
    let closure_class = b.valk_class("core", "Closure")
    let outer_func = generate_closure_outer_function(parent_func, fi, data_class)

    // Init closure class instance
    let values_closure = Map[Value].new()
    values_closure.set("inner_func", fptr)
    values_closure.set("outer_func", vgen_func_ptr(outer_func))
    values_closure.set("data", data)

    // Generate correct function info
    let clo_fi = outer_func.info()
    clo_fi = clo_fi.clone()
    let new_args = Array[Type].new()
    each clo_fi.args as i, type {
        if i == 0 : continue
        new_args.push(type)
    }
    clo_fi.args = new_args

    // 
    let v = vgen_class_init(closure_class, decl_scope, values_closure)
    v = vgen_cast(v, type_closure(b, clo_fi))
    return v
}

fn generate_closure_outer_function(parent: Func, fi: FuncInfo, data_class: ?Class) Func {
    let u = parent.unit
    let b = u.build
    let func = u.new_func(parent.fc, act_public, null, null, null)

    // Define function arguments
    let closure_class = b.valk_class("core", "Closure")
    let data_arg = func.add_arg("closure_obj", closure_class.get_type())
    let limit = fi.args.length - (isset(data_class) ? data_class.props.length() : 0)
    each fi.args as i, arg_type {
        if i == limit : break
        func.add_arg("arg" + i, arg_type)
    }
    // Return types
    each fi.rett_types as i, rett {
        func.add_rett(rett)
    }
    if func.rett_types.length > 0 {
        func.scope.must_return = true
    }
    // Errors
    func.can_error = fi.can_error
    func.errors = fi.errors

    ////////////////////////
    // AST
    ////////////////////////

    func.init_ast_scope()
    let ast = func.scope.ast

    // Read data
    let prop_inner = closure_class.props.get("inner_func") ! b.error("Missing closure 'inner_func' property (bug)")
    let inner_func = vgen_prop(prop_inner, vgen_decl(data_arg.decl))
    inner_func = vgen_cast(inner_func, type_func_info(b, fi))

    // Push arguments
    let inner_values = array[Value]{}
    each func.args as i, arg {
        if i == 0 : continue
        inner_values.push(vgen_decl(arg.decl))
    }

    // Push bind values
    if isset(data_class) {
        let prop_data = closure_class.props.get("data") ! b.error("Missing closure 'data' property (bug)")
        let on = vgen_prop(prop_data, vgen_decl(data_arg.decl))
        on = vgen_cached(on)
        each data_class.props as name, prop {
            inner_values.push(vgen_prop(prop, on))
        }
    }

    // Call inner function
    let inner_call = vgen_func_call(b, func.scope, inner_func, inner_values)

    if fi.can_error {
        // AST throw error
        let sub = func.scope.sub_scope(sc_default)
        let ast = sub.ast

        let g = b.valk_global("core", "error_code")
        let gmsg = b.valk_global("core", "error_msg")

        ast_gen_throw(b, sub, vgen_global(g), vgen_global(gmsg))

        // Error handler value
        let on = inner_call
        inner_call = Value { 
            type: v_errh
            errh: ErrorHandler {
                type: errh_break
                on: on
                scope: sub
                decl_err: null
                decl_msg: null
            }
            multi_values: on.multi_values
            rett: on.rett
        }
    }

    // Return value
    if fi.rett_types.length > 0 {
        ast_gen_return(func, func.scope, array[Value]{ inner_call })
    } else {
        ast.push(Token {
            type: tk_statement
            value1: inner_call
        })
        ast_return_value(func, func.scope, null)
    }

    func.finish_ast_scope()
    func.parsed_ast = true

    func_ir(func)

    return func
}

fn closure_gen_data_class(b: Build, u: Unit, bind_values: Array[Value]) Class {

    let dclass = u.new_class(null, act_public, null, ct_class, null, null)

    each bind_values as i, val {
        let name = "data_" + i
        let prop = Prop {
            name: name
            type: val.rett
            class: dclass
            act: act_public
        }
        dclass.props.set(name, prop)
    }

    dclass.determine_size() ! b.error("Cannot determine closure data class size (bug)")

    return dclass
}
