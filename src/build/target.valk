
use helper

struct Target {
    host_os: String
    host_arch: String
    os: String
    arch: String
    target: String
    ptr_size: int

    fn host_is_target() bool {
        return this.host_os == this.os && this.host_arch == this.arch
    }
    fn target_is_win() bool {
        return this.os == "win"
    }
    fn target_is_linux() bool {
        return this.os == "linux"
    }
    fn target_is_macos() bool {
        return this.os == "macos"
    }
    fn target_is_x64() bool {
        return this.arch == "x64"
    }
    fn target_is_arm64() bool {
        return this.arch == "arm64"
    }
}

fn get_target(cli: helper:CLIArgs) Target {
    let host_os = default_os()
    let host_arch = default_arch()
    let os = host_os
    let arch = host_arch

    let target = cli.get_first_values_for("--target") ? (os + "-" + arch)

    let valid = array[String]{ "win-x64", "linux-x64", "linux-arm64", "macos-x64", "macos-arm64" }
    if !valid.contains(target) {
        let supported = ""
        each valid as i,t {
            if i > 0 : supported += ", "
            supported += t
        }
        build_error("Invalid target: " + target + "\nSupported: " + supported)
    }

    let split = target.split("-")
    os = split.get(0) ! exit(1)
    arch = split.get(1) ! exit(1)

    let ptr_size = 8

    return Target {
        host_os: host_os
        host_arch: host_arch
        os: os
        arch: arch
        target: target
        ptr_size: ptr_size
    }
}

fn default_os() String {
    #if OS == win
    return "win"
    #elif OS == linux
    return "linux"
    #elif OS == macos
    return "macos"
    #end
    return "unknown"
}
fn default_arch() String {
    #if ARCH == x64
    return "x64"
    #elif ARCH == arm64
    return "arm64"
    #end
    return "unknown"
}
