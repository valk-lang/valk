
use helper
use valk:fs
use valk:os

fn stage_link(b: Build) {

    if b.verbose > 2 : b.log("> Stage 4: Link executable")

    let t = b.target
    let host_is_target = t.os == t.host_os && t.arch == t.host_arch
    let valk_lib_dir = helper:valk_lib_dir()

    let linker = ""
    if host_is_target && t.os != "win" {
        linker = "ld"
    } else {
        link_check_symlinks(b, t)

        if t.os == "linux" {
            linker = "ld.lld"
        } else if t.os == "macos" {
            linker = "ld64.lld"
        } else if t.os == "win" {
            linker = "lld-link"
        }

        if t.host_os == "win" {
            linker = fs:exe_dir() + "/" + linker + ".exe"
        }
    }
    if linker.bytes == 0 : b.error("Cannot determine linker for your OS")

    let cmd = linker
    cmd += " "
    if t.os == "win" {
        cmd += "/out:\""
    } else {
        cmd += "-pie -o \""
    }
    cmd += b.path_out
    cmd += "\" "

    // Link directories
    let link_dirs = array[String]{}
    // Default OS directories
    if t.host_is_target() {
        if t.target_is_macos() {
            link_dirs.push("/usr/local/lib")
            link_dirs.push("/opt/homebrew/lib")
            link_dirs.push("/System/Library/Frameworks")
            link_dirs.push("/Library/Frameworks")
            link_dirs.push("~/Library/Frameworks")
            link_dirs.push("/Library/Developer/CommandLineTools/SDKs/MacOSX.sdk/usr/lib/")
            link_dirs.push("/usr/lib") // Some macos environments do not allow you to link with these libraries
        } else if t.target_is_linux() {
            link_dirs.push("/lib64");
            link_dirs.push("/usr/lib64");
            link_dirs.push("/usr/local/lib64");
            link_dirs.push("/usr/lib");
            link_dirs.push("/usr/local/lib");
            if t.target_is_x64() {
                link_dirs.push("/lib/x86_64-linux-gnu");
            } else if t.target_is_arm64() {
                link_dirs.push("/lib/aarch64-linux-gnu");
            }
        }
    }

    // Add package link directories
    each b.pkc_by_dir as pkgdir, pkc {
        link_dirs.push(pkgdir + "/libs/" + t.target)

        // Add extra directories from config
        let config = pkc.config
        if !isset(config) : continue
        let data = config.json
        data = data.get("targets") ! continue
        data = data.get(t.target) ! continue
        let ldirs = data.get("link-dirs") ! continue
        let dirs = ldirs.array() ! continue

        each dirs as dir {
            let d = dir.string() ! continue
            link_dirs.push(pkgdir + "/" + d)
        }
    }

    // Cli args
    let cli = b.cli
    let cli_link_dirs = cli.values.get("-L") ? null
    if isset(cli_link_dirs) {
        each cli_link_dirs as dir {
            link_dirs.push(dir)
        }
    }

    // Add dirs
    each link_dirs as dir {
        cmd = add_lib_dir(t, cmd, dir)
    }

    // Details
    if t.os == "linux" {
        // cmd += "--sysroot=\""
        // cmd += valk_lib_dir
        // cmd += "/root\" "
        cmd += "--sysroot=\"/\" "

        if t.arch == "x64" {
            cmd += "-m elf_x86_64 "
            cmd += "--dynamic-linker /lib64/ld-linux-x86-64.so.2 "
        } else if t.arch == "arm64" {
            cmd += "-m aarch64linux "
            cmd += "--dynamic-linker /lib/ld-linux-aarch64.so.1 "
        }

        // if (b->type == build_t_exe) {
            cmd += "-l:Scrt1.o "
            cmd += "-l:crti.o "
            cmd += "-l:crtbeginS.o "
        // } else if (b->type == build_t_shared_lib) {
        //     str_append_chars(cmd, "--shared ");
        // }

    } else if t.os == "macos" {
        // cmd += "-syslibroot "
        // cmd += valk_lib_dir
        // cmd += "root "
        cmd += "-syslibroot \"/\" "

        // ppc, ppc64, i386, x86_64
        if t.arch == "x64" {
            cmd += "-arch x86_64 "
        } else if t.arch == "arm64" {
            cmd += "-arch arm64 "
        }
        cmd += "-platform_version macos 11.0 15.0 "
        // str_append_chars(cmd, "-sdk_version 11.1 ");
        // -macosx_version_min 11.1.0 -sdk_version 11.1.0

        // if (b->type == build_t_shared_lib) {
        //     str_append_chars(cmd, "-dylib ");
        // }
    } else if t.os == "win" {
        // /winsysroot:<value>
        cmd += "/nodefaultlib "
        cmd += "/guard:ehcont "
        // str_append_chars(cmd, "/force:unresolved ");
        if t.arch == "x64" {
            cmd += "/machine:x64 "
        }
        // if (b->type == build_t_shared_lib) {
        //     str_append_chars(cmd, "/dll ");
        // }
    }

    // Object files
    each b.units as unit {
        cmd += unit.path_o + " "
    }

    // Link libs
    let not_found = array[String]{}
    cmd = add_libs(b, cmd, link_dirs, not_found)

    // End
    if t.os == "linux" {
        cmd += "-l:crtendS.o "
        cmd += "-l:crtn.o "
    }

    // Run command
    if b.verbose > 1 : println("# Link cmd: " + cmd);

    if b.verbose > 1 {
        each not_found as name {
            println("> Warn: Library not found: " + name);
        }
        if not_found.length == 0 : println("✅ All libraries found")
    }

    let code, out = os:exec(cmd) ? (1, "")
    if code != 0 {
        println("-----------------------------")
        println("# Cmd: " + cmd)
        println("# Exit code: " + code)
        println("# Output: " + out)
        println("-----------------------------")
        b.error("❌ Failed to link")
    }
}

fn add_libs(b: Build, cmd: String, dirs: Array[String], not_found: Array[String]) String {

    // Links from .valk.h headers
    let links = b.links
    each links as link {
        let lib_path = lib_path(b, link.lib, dirs, link.type == link_static, not_found) ! continue
        cmd = add_lib(b, cmd, link.lib, lib_path)
    }

    let is_static = b.is_static
    let cli = b.cli
    let libs = cli.values.get("-l") ? null
    if isset(libs) {
        each libs as name {
            let lib_path = lib_path(b, name, dirs, is_static, not_found) ! continue
            cmd = add_lib(b, cmd, name, lib_path)
        }
    }

    return cmd
}

fn lib_path(b: Build, name: String, dirs: Array[String], prefer_static: bool, not_found: Array[String]) String !not_found {

    let t = b.target
    let exts = array[String]{}
    let prefix = ""
    if t.target_is_win() {
        exts.push(".dll")
        exts.push(".lib")
    } else if t.target_is_macos() {
        exts.push(".tbd")
        exts.push(".dylib")
        exts.push(".a")
        prefix = "lib"
    } else {
        exts.push(".so")
        exts.push(".a")
        prefix = "lib"
    }
    if prefer_static : exts.reverse()
    exts.prepend(".o")

    each dirs as dir {
        let base = dir + "/"
        each exts as ext {

            let fn = name
            let fixed_name = name.starts_with(":")
            if fixed_name : fn = fn.sub_str(1, fn.bytes - 1)
            else: fn = prefix + name + ext

            let full = base + fn
            if fs:exists(full) : return full
            if fixed_name : break
        }
    }

    not_found.push(name)
    throw not_found
}

fn add_lib(b: Build, cmd: String, name: String, path: String) String {
    let t = b.target
    if t.target_is_macos() && t.host_is_target() {
        cmd += "-l" + name + " "
    } else {
        cmd += path + " "
    }
    return cmd
}
fn add_lib_dir(t: Target, cmd: String, dir: String) String {
    // Link directories
    if fs:exists(dir) {
        cmd += t.os == "win" ? "/libpath:\"" : "-L\""
        cmd += dir
        cmd += "\" "
    }
    return cmd
}

fn link_check_symlinks(b: Build, t: Target) {
    let linker_dir = fs:exe_dir()
    if t.host_os != "win" {
        let code, out = os:exec("which lld") ! b.error("❌ Please install the 'lld' linker to cross compile")
        if code == 1 : b.error("❌ Please install the 'lld' linker to cross compile")
        linker_dir = out.rtrim("/lld\n")
    }

    if !fs:is_dir(linker_dir) : b.error("❌ Linker directory not found: " + linker_dir)
    let ext = t.host_os == "win" ? ".exe" : ""
    let main_linker = linker_dir + (t.host_os == "win" ? "/lld-link" : "/lld") + ext
    if !fs:exists(main_linker) : b.error("❌ Cannot find linker: " + main_linker)

    if t.os == "linux" : check_linker_exe(b, t, main_linker, linker_dir + "/ld.lld" + ext)
    if t.os == "macos" : check_linker_exe(b, t, main_linker, linker_dir + "/ld64.lld" + ext)
    if t.os == "win" : check_linker_exe(b, t, main_linker, linker_dir + "/lld-link" + ext)
}

fn check_linker_exe(b: Build, t: Target, main_linker: String, alias_path: String) {
    if !fs:exists(alias_path) {
        let cmd = "valk symlink-linker"
        if t.host_os != "win" {
            cmd = "sudo " + cmd
        }
        b.error("> Run the following command to generate symlinks to lld in order to cross compile: " + cmd)
    }
}
