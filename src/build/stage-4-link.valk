
use helper
use valk:fs
use valk:core

fn stage_link(b: Build) {

    if b.is_ir() : return

    if b.verbose > 2 : b.log("> Stage 4: Link executable")

    let t = b.target
    let host_is_target = t.os == t.host_os && t.arch == t.host_arch

    let linker = ""
    if host_is_target && t.os != "win" {
        linker = "ld"
    } else {
        link_check_symlinks(b, t)

        if t.os == "linux" {
            linker = "ld.lld"
        } else if t.os == "macos" {
            linker = "ld64.lld"
        } else if t.os == "win" {
            linker = "lld-link"
        }

        if t.host_os == "win" {
            linker = helper:add(fs:exe_dir(), linker + ".exe")
        }
    }
    if linker.bytes == 0 : b.error("Cannot determine linker for your OS")

    let cmd = linker
    cmd += " "
    if t.os == "win" {
        cmd += "/stack:8388608,65536 " // 8MB stack, 65k commit size
        if b.debug : cmd += "/DEBUG "
        cmd += "/out:\""
    } else {
        cmd += "-pie -o \""
    }
    cmd += b.path_out
    cmd += "\" "

    // Link directories
    let link_dirs = Array[String]{}
    // Default OS directories
    if t.host_is_target() {
        if t.target_is_macos() {
            link_dirs.append("/usr/local/lib")
            link_dirs.append("/opt/homebrew/lib")
            link_dirs.append("/System/Library/Frameworks")
            link_dirs.append("/Library/Frameworks")
            link_dirs.append("~/Library/Frameworks")
            link_dirs.append("/Library/Developer/CommandLineTools/SDKs/MacOSX.sdk/usr/lib/")
            link_dirs.append("/Library/Developer/CommandLineTools/SDKs/MacOSX.sdk/System/Library/Frameworks")
            link_dirs.append("/usr/lib") // Some macos environments do not allow you to link with these libraries
        } else if t.target_is_linux() {
            link_dirs.append("/lib64");
            link_dirs.append("/usr/lib64");
            link_dirs.append("/usr/local/lib64");
            link_dirs.append("/usr/lib");
            link_dirs.append("/usr/local/lib");
            if t.target_is_x64() {
                link_dirs.append("/lib/x86_64-linux-gnu");
            } else if t.target_is_arm64() {
                link_dirs.append("/lib/aarch64-linux-gnu");
            }
        } else if t.target_is_win() {
            core:getenv("SystemRoot") -> windir {
                link_dirs.append(windir + "\\System32");
            } 
        }
    }

    // Add package link directories
    each b.pkc_by_dir as pkc, pkgdir {
        if pkc.use_libs == LIBS.none : continue

        let prepend = pkc.use_libs == LIBS.prefer

        if prepend : link_dirs.prepend(helper:add(helper:add(pkgdir, "libs"), t.target))
        else : link_dirs.append(helper:add(helper:add(pkgdir, "libs"), t.target))

        // Add extra directories from config
        let config = pkc.config
        if !isset(config) : continue
        let dirs = config.json.get("targets").get(t.target).get("link-dirs").array()

        each dirs as dir {
            let d = dir.string()
            if prepend : link_dirs.prepend(helper:add(pkgdir, d))
            else : link_dirs.append(helper:add(pkgdir, d))
        }
    }

    // Cli args
    let cli = b.cli
    let cli_link_dirs = cli.values.get("-L") !? null
    if isset(cli_link_dirs) {
        each cli_link_dirs as dir {
            link_dirs.append(dir)
        }
    }

    // Add dirs
    each link_dirs as dir {
        cmd = add_lib_dir(t, cmd, dir)
    }

    // Details
    if t.os == "linux" {
        cmd += "--sysroot=\"/\" "

        if t.arch == "x64" {
            cmd += "-m elf_x86_64 "
            cmd += "--dynamic-linker /lib64/ld-linux-x86-64.so.2 "
        } else if t.arch == "arm64" {
            cmd += "-m aarch64linux "
            cmd += "--dynamic-linker /lib/ld-linux-aarch64.so.1 "
        }

        cmd += "-l:Scrt1.o "
        cmd += "-l:crti.o "
        cmd += "-l:crtbeginS.o "
        // if target is shared library:
        // str_append_chars(cmd, "--shared ");

    } else if t.os == "macos" {
        cmd += "-syslibroot \"/\" "

        if t.arch == "x64" {
            cmd += "-arch x86_64 "
        } else if t.arch == "arm64" {
            cmd += "-arch arm64 "
        }
        cmd += "-platform_version macos 11.0 15.0 "
        // if target is shared library:
        // str_append_chars(cmd, "-dylib ");
    } else if t.os == "win" {
        // /winsysroot:<value>
        cmd += "/nodefaultlib "
        cmd += "/guard:ehcont "
        // str_append_chars(cmd, "/force:unresolved ");
        if t.arch == "x64" {
            cmd += "/machine:x64 "
        }
        // if target is shared library:
        // str_append_chars(cmd, "/dll ");
    }

    // Object files
    each b.units as unit {
        if !unit.is_used : continue
        cmd += unit.path_o + " "
    }

    // Link libs
    let not_found = Array[String]{}
    cmd = add_libs(b, cmd, link_dirs, not_found)

    // End
    if t.os == "linux" {
        cmd += "-l:crtendS.o "
        cmd += "-l:crtn.o "
    }

    // Run command
    if b.verbose > 1 : println("# Link cmd: " + cmd);

    if b.verbose > 1 {
        each not_found as name {
            println("> Warn: Library not found: " + name);
        }
        if not_found.length == 0 : helper:msg("✅", "All libraries found")
    }

    let code, out = core:exec(cmd)
    if code != 0 {
        println("-----------------------------")
        println("# Cmd: " + cmd)
        println("# Exit code: " + code)
        println("# Output: " + out)
        println("-----------------------------")
        b.error_emoji("❌", "Failed to link")
    }
}

fn add_libs(b: Build, cmd: String, dirs: Array[String], not_found: Array[String]) String {

    // Only include links from namespaces that are used
    let used_links = Array[Link]{}
    each b.units as unit {
        if !unit.is_used : continue
        each unit.links as link {
            used_links.append(link, true)
        }
    }

    // Links
    let links = b.links
    each links as link {
        if !used_links.contains(link) : continue

        if link.type == LINK.framework {
            cmd += "-framework \"" + link.lib + "\" "
            continue
        }

        cmd = add_lib(b, cmd, link.lib, dirs, not_found)
        // cmd = add_lib(b, cmd, link.lib, lib_path)
        // let is_static = (b.is_static && link.type != LINK.dynamic) || link.type == LINK.static
        // let lib_path = lib_path(b, link.lib, dirs, is_static, not_found) ! continue
        // cmd = add_lib(b, cmd, link.lib, lib_path)
    }

    let is_static = b.is_static
    let cli = b.cli
    let libs = cli.values.get("-l") !? null
    if isset(libs) {
        each libs as name {
            cmd = add_lib(b, cmd, name, dirs, not_found)
            // let lib_path = lib_path(b, name, dirs, is_static, not_found) ! continue
            // cmd = add_lib(b, cmd, name, lib_path)
        }
    }

    return cmd
}

fn add_lib(b: Build, cmd: String, name: String, dirs: Array[String], not_found: Array[String]) String {
    if b.target.target_is_win() {
        if name.starts_with(":") {
            // Remove `:` on windows
            name = name.part(1, name.length)
        } else if !name.ends_with(".o") {
            name += ".lib"
        }
        cmd += "\"" + name + "\" "
    } else if b.target.target_is_macos() {
        // Remove `:` on macos
        if name.starts_with(":") {
            name = name.part(1, name.length)
        }
        // Macos ld doesnt look for .o files in link directories
        if name.ends_with(".o") {
            let path = lib_path(b, name, dirs, b.is_static, not_found) ! return cmd
            cmd += "\"" + path + "\" "
            return cmd
        }
        cmd += "-l\"" + name + "\" "
    } else {
        cmd += "-l\"" + name + "\" "
    }
    return cmd
}

fn lib_path(b: Build, name: String, dirs: Array[String], prefer_static: bool, not_found: Array[String]) String !not_found {

    each dirs as dir {
        let full = helper:add(dir, name)
        if fs:exists(full) : return full
    }

    not_found.append(name)
    throw not_found
}

fn add_lib_dir(t: Target, cmd: String, dir: String) String {
    // Link directories
    let is_framework = t.target_is_macos() && dir.ends_with("Frameworks")
    if is_framework {
        cmd += "-F\""
    } else {
        cmd += t.target_is_win() ? "/libpath:\"" : "-L\""
    }

    cmd += dir
    cmd += "\" "
    return cmd
}

fn link_check_symlinks(b: Build, t: Target) {
    let linker_dir = fs:exe_dir()
    if t.host_os != "win" {
        let code, out = core:exec("which lld")
        if code == 1 : b.error_emoji("❌", "Please install the 'lld' linker to cross compile")
        linker_dir = out.trim("\n").rtrim("/lld")
    }

    if !fs:is_dir(linker_dir) : b.error_emoji("❌", "Linker directory not found: " + linker_dir)
    let ext = t.host_os == "win" ? ".exe" : ""
    let main_linker = helper:add(linker_dir, (t.host_os == "win" ? "lld-link" : "lld") + ext)
    if !fs:exists(main_linker) : b.error_emoji("❌", "Cannot find linker: " + main_linker)

    if t.os == "linux" : check_linker_exe(b, t, main_linker, helper:add(linker_dir, "ld.lld" + ext))
    if t.os == "macos" : check_linker_exe(b, t, main_linker, helper:add(linker_dir, "ld64.lld" + ext))
    if t.os == "win" : check_linker_exe(b, t, main_linker, helper:add(linker_dir, "lld-link" + ext))
}

fn check_linker_exe(b: Build, t: Target, main_linker: String, alias_path: String) {
    if !fs:exists(alias_path) {
        let cmd = "valk symlink-linker"
        if t.host_os != "win" {
            cmd = "sudo " + cmd
        }
        b.error("> Run the following command to generate symlinks to lld in order to cross compile: " + cmd)
    }
}
