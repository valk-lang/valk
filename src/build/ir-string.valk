

fn ir_string(ir: IR, str: Str) {

    if !str.is_used : return
    if str.ir_defined : return
    str.ir_defined = true

    let body = str.body
    let code = ir.code_strings

    let bytes = body.bytes
    let data = body.data

    // 
    code.append_str("@string.")
    code.append_uint(str.nr)
    code.append_str(" = private global <{ i64, i64, i64, i64, i32, i16, i16, i64, [")
    code.append_str(bytes + 1)
    code.append_str(" x i8] }> <{ i64 0, i64 0, i64 0, i64 0, i32 0, i16 0, i16 32, i64 ")
    code.append_str(bytes)
    code.append_str(", [")
    code.append_str(bytes + 1)
    code.append_str(" x i8] c\"")
    // String bytes
    let i : uint = 0
    while i < bytes {
        let ch = @ptrv(data, u8, i++)
        if !ch.is_alpha_numeric() { 
            code.append_byte('\\')
            let nr : String = ch.to_hex()
            if nr.bytes < 2 : code.append_byte('0')
            code.append_str(nr)
            continue
        }
        code.append_byte(ch)
    }
    code.append_str("\\00\"")
    //
    code.append_str(" }>\n")
}
