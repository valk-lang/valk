
use helper

fn read_ast(p: Parser, scope: Scope, single: bool) {

    let first = true

    let func = p.ctx.getFunc()
    let fast = func.fast()
    let func_buffer_count : uint = fast.buffers_used.length

    while true {

        fast.free_buffers(func_buffer_count)

        if scope.did_return {
            if !single : p.skip_body("}")
            if scope.did_exit {
                // Insert return statement after calling exit-function
                let func = p.func()
                scope.ast.append(Token { type: AST.unreachable })
            }
            break
        }

        if single && !first : break

        p.backup()
        let t = p.tok(true, true)
        first = false

        if t == TOK.sign {
            if p.word_is("}") {
                break
            }
            if p.word_is(";") {
                continue
            }
            if p.word_is("#") && p.on_newline {
                parse_compile_macro(p, scope)
                continue;
            }
            if p.word_is("{") {
                let sub = scope.sub_scope(SCOPE.default)
                read_ast(p, sub, false)
                if sub.did_return : scope.did_return
                scope.ast.append(Token { type: AST.inline_scope, scope1: sub })
                continue
            }
        }
        if t == TOK.word {
            if p.word_is("let") {
                ast_let(p, scope)
                continue
            }
            if p.word_is("return") {
                ast_return(p, scope)
                continue
            }
            if p.word_is("while") {
                ast_while(p, scope)
                continue
            }
            if p.word_is("each") {
                ast_each(p, scope)
                continue
            }
            if p.word_is("if") {
                ast_if(p, scope)
                continue
            }
            if p.word_is("throw") {
                ast_throw(p, scope)
                continue
            }
            if p.word_is("cothrow") {
                ast_cothrow(p, scope)
                continue
            }
            if p.word_is("break") {
                ast_break(p, scope)
                continue
            }
            if p.word_is("continue") {
                ast_continue(p, scope)
                continue
            }
            if p.word_is("ir_comment") {
                ast_ir_comment(p, scope)
                continue
            }
        }
        if t == TOK.at_word {
            if p.word_is("@return_empty") {
                scope.did_return = true
                scope.did_exit = true
                continue
            }
            if p.word_is("@allocas") {
                let func = p.func()
                if !func.is_entrance : p.error("@allocas can only be used inside a function thats flagged with $entrance")
                // scope.ast.append(Token { type: AST.allocas })
                scope.ast.append(Token { type: AST.inline_scope, scope1: func.fast().scope_alloca })
                continue
            }
        }

        // Not a token -> Read value instead
        p.restore()
        let left = read_value(p, scope)

        t = p.tok(true, true, false)
        if t == TOK.sign {
            if p.word_is("=") || p.word_is("+=") || p.word_is("-=") || p.word_is("*=") || p.word_is("/=") || p.word_is("%=") || p.word_is("^=") || p.word_is("&=") || p.word_is("|=") {
                t = p.tok(true, true)

                if left.is_multi() : p.error("You cannot assign multi values at once")

                let assign_chunk = p.clone_chunk()

                let op = OP.none
                if p.word_is("+=") : op = OP.add
                else if p.word_is("-=") : op = OP.sub
                else if p.word_is("*=") : op = OP.mul
                else if p.word_is("/=") : op = OP.div
                else if p.word_is("%=") : op = OP.mod
                else if p.word_is("^=") : op = OP.bit_xor
                else if p.word_is("&=") : op = OP.bit_and
                else if p.word_is("|=") : op = OP.bit_or

                p.set_suggest(left.rett)
                p.assign_to = left
                let right = read_value(p, scope)
                p.pop_suggest()

                // let lefts = left.unroll() 
                // let rights = right.unroll() 

                // each lefts as left, i {
                //     let right = rights.get(i) ! p.error("Right side is not returning enough values to fit all the values on the left")

                    if left.type == VAL.prop {
                        let on = left.value1
                        if isset(on) {
                            if on.rett.is_imut {
                                p.error("Cannot assign a value to a immutable property")
                            }
                        }
                    }

                    left.check_decl_mutable()

                    if op != OP.none {
                        right = value_handle_op(p, scope, op, left, right)
                    }

                    ast_gen_assign(p.ctx, scope, left, right, false, assign_chunk)
                // }

                // Left over values
                // let left_overs = rights.length - lefts.length
                // let index = lefts.length

                // while left_overs-- > 0 {
                //     let right = rights.get(index) ! p.error("Compiler bug: cannot find left over statement for assign")
                //     scope.ast.append(Token {
                //         type: AST.statement
                //         value1: right
                //     })
                // }

                continue
            }
        }

        scope.ast.append(Token {
            type: AST.statement
            value1: left
        })
    }

    if scope.must_return && !scope.did_return {
        p.error("Missing 'return' statement")
    }

    // Add continue add end of loop
    if scope.type == SCOPE.loop && !scope.did_return {
        ast_continue(p, scope)
    }

    // Return void
    if scope.type == SCOPE.func {
        if !scope.did_return {
            let func = p.func()
            let values = Array[Value]{}
            each func.rett_real.unroll() as type {
                values.append(vgen_empty_value(type) ?! continue)
            }
            ast_return_value(func, scope, vgen_grouped_values(p.build, values))
        }
    }
}

fn ast_let(p: Parser, scope: Scope) {
    let names = Array[String]{}
    let decls = Array[?Decl]{}

    let first = true
    while p.sign_is(",") || first {
        if !first {
            p.tok(true, false)
        }
        first = false

        let name = p.read_word(true, true)
        names.append(name)
        // Type
        p.tok(true, false, false)
        if p.sign_is(":") {
            p.tok(true, false)
            let type = read_type(p, scope, true)
            let decl = Decl.new(type, false)
            decl.name = name
            decl.chunk = p.clone_chunk()
            decls.append(decl)
        } else {
            decls.append(null)
        }

        p.tok(true, false, false)
    }

    p.expect("=", true, true)

    let err_chunk = p.clone_chunk()
    let suggest_type = p.suggest_type
    if decls.length == 1 {
        let decl = decls.get(0) !? null
        if isset(decl) {
            p.assign_to = vgen_decl(decl)
            p.suggest_type = decl.type
        }
    } else {
        let values = Array[Value]{}
        each decls as decl {
            if isset(decl) : values.append(vgen_decl(decl))
            else : vgen_null(p.build.valk_type("type", "ptr"))
        }
        let grouped = vgen_grouped_values(p.build, values)
        p.assign_to = grouped
        p.suggest_type = grouped.rett
    }

    let v = read_value(p, scope)
    p.suggest_type = suggest_type

    // Init value
    scope.ast.append(tgen_statement(v))
    //
    let values = v.unroll()

    if names.length > values.length {
        p.set_chunk(err_chunk)
        p.error("Right side of '=' is not returning enough values to fit all your variables")
    }

    each names as n, i {
        let name : ?String = null
        if n != "_" : name = n

        let decl = decls.get(i) !? null
        let value = values.get(i) ! p.error("Missing value (bug)")

        if value.rett.is_multi() {
            p.error("You cannot assign multiple values to a single variable")
        }

        if !isset(decl) {
            // if value.rett.is_inline_type() : value = vgen_ref(p.ctx, value, scope) 
            decl = Decl.new(value.rett, false)
            decl.name = name
            decl.chunk = p.clone_chunk()
        }
        if decl.type.is_void() : p.error("You cannot declare a variable of type 'void'")

        if isset(name) : scope.set_idf(p, name, Idf.for_decl(decl))
        scope.add_decl(decl)

        if decl.type.nullable && !value.rett.nullable {
            scope.not_null_add(decl)
        }
        
        ast_gen_assign(p.ctx, scope, vgen_decl(decl), value, true, err_chunk)
        decl.is_used = false
    }
}

fn ast_return(p: Parser, scope: Scope) {

    let for_scope = scope.find_type2(SCOPE.func, SCOPE.vscope) ! p.error("Unexpected 'return' statement")
    if for_scope.type == SCOPE.vscope {
        ast_vscope_return(p, scope, for_scope)
        return
    }

    let func = p.func()
    let rettc = func.rett.count_types()
    if rettc == 0 {
        let values = Array[Value]{}
        each func.rett_real.unroll() as type {
            values.append(vgen_empty_value(type) ?! continue)
        }
        ast_return_value(func, scope, vgen_grouped_values(p.build, values))
    } else {
        let st = p.suggest_type
        p.suggest_type = func.rett
        let retv = read_value(p, scope)
        retv = retv.try_convert(p.ctx, scope, func.rett)
        p.suggest_type = st
        ast_gen_return(p.ctx, func, scope, retv)
    }
}

fn ast_vscope_return(p: Parser, scope: Scope, vscope: Scope) {

    // Types
    let define_decls = false
    let decls = p.vscope_decls ?? <{
        define_decls = true
        let list = Array[Decl]{}
        p.vscope_decls = list
        return list
    }

    // Read values
    let values = Array[Value]{}
    let first = true
    while p.word_is(",") || first {

        if !first : p.tok(true, true)
        first = false

        if !define_decls {
            if values.length == decls.length {
                p.error("Too many return values")
            }
        }
        let v = read_value(p, scope)
        if define_decls {
            let decl = Decl.new(v.rett, false)
            decl.is_mut = true
            scope.add_decl(decl)
            decls.append(decl)
        } else {
            let decl = decls.get(values.length) ! p.error("Value scope - cannot find variable (bug)")
            decl.type.compat_check(v.rett, p.ctx)
        }

        let decl = decls.get(values.length) ! p.error("Value scope | cannot find variable (bug)")

        values.append(v)

        ast_gen_assign(p.ctx, scope, vgen_decl(decl), v, false)

        p.tok(true, true, false)
    }

    if values.length < decls.length {
        p.error("Missing values. You returned fewer values compared to your previous return statement")
    }

    scope.ast.append(Token {
        type: AST.vscope_return
    })

    scope.did_return = true
}

fn ast_while(p: Parser, scope: Scope) {

    let func = scope.get_func() ! p.error("Missing function scope for while loop (bug)")
    let fast = func.fast()

    let fp = p.loop_first_parse
    p.loop_first_parse = true

    let start = p.clone_chunk()
    p.set_chunk(start)

    // Stage 1
    let sub = scope.sub_scope(SCOPE.loop)

    let cond = read_value(p, sub)
    if !cond.rett.is_bool() : p.error("The while condition value must be of type bool, not: " + cond.rett)
    p.expect2("{", ":", true, true)
    let single = p.word_is(":")

    apply_issets(sub, cond.issets)
    read_ast(p, sub, single)

    // Remove decls from func scope
    p.loop_first_parse = fp
    each sub.decls as decl {
        if decl.is_buffer : continue
        fast.scope.decls.remove_value(decl)
    }
    let decls_that_are_null_at_start = sub.null_at_start

    // Stage 2
    p.set_chunk(start)
    sub = scope.sub_scope(SCOPE.loop)

    each decls_that_are_null_at_start as decl {
        sub.not_null_remove(decl)
    }

    cond = read_value(p, sub)
    if !cond.rett.is_bool() : p.error("The while condition value must be of type bool, not: " + cond.rett)
    p.expect2("{", ":", true, true)
    single = p.word_is(":")

    apply_issets(sub, cond.issets)
    read_ast(p, sub, single)

    scope.not_null_remove_check(sub)

    each decls_that_are_null_at_start as decl {
        scope.not_null_remove(decl)
    }

    if p.loop_first_parse {
        each sub.decls as decl {
            if decl.is_buffer : continue
            fast.scope.decls.remove_value(decl)
        }
    }

    each sub.null_at_end as decl {
        scope.not_null_remove(decl)
    }

    let cond_not_issets = cond.not_issets
    if isset(cond_not_issets) : apply_issets(scope, cond_not_issets)

    scope.ast.append(Token {
        type: AST.while
        value1: cond
        scope1: sub
    })
}

fn ast_each(p: Parser, scope: Scope) {

    let on = read_value(p, scope)

    // Stage 1
    let fp = p.loop_first_parse
    p.loop_first_parse = true

    let start = p.clone_chunk()
    let loop = ast_each_stage(p, scope, on, null)

    // Stage 2
    p.loop_first_parse = fp
    p.set_chunk(start)
    loop = ast_each_stage(p, scope, on, loop.null_at_start)

    each loop.null_at_end as decl {
        scope.not_null_remove(decl)
    }

    // Final
    scope.ast.append(Token {
        type: AST.each
        scope1: loop
    })
}

fn ast_each_stage(p: Parser, scope: Scope, on: Value, null_at_start: ?Array[Decl]) Scope {
    let pfunc = p.func()
    let pfast = pfunc.fast()

    let class = on.rett.get_class() !? null
    if !isset(class) || on.rett.nullable : p.error("Cannot use 'each' on type: " + on.rett)
    let func = class.get_func("each_get") ! p.error("Cannot use 'each' on type: '" + on.rett + "'. This type does not have an 'each_get' function")

    p.expect("as", true, true)

    // Read variable names
    let names = Array[String]{}
    let single = false
    while true {
        let name = p.read_word(true, true) 
        if names.contains(name) : p.error("Duplicate variable name: " + name)
        names.append(name)
        p.expect3(",", "{", ":", true, true)
        if p.word_is(",") : continue
        single = p.word_is(":")
        break
    }

    // Check if too many variables
    let rettcount = func.rett.count_types()
    if names.length > rettcount + 1 : p.error("Too many variable names")
    let index_name : ?String = null
    if names.length == rettcount + 1 : index_name = names.pop_last() ! p.error("Missing index variable name (bug)")

    // Create scope
    let sub = scope.sub_scope(SCOPE.loop)

    if isset(null_at_start) {
        each null_at_start as decl {
            sub.not_null_remove(decl)
        }
    }

    // Set decls
    let idecl = Decl.new(type_uint(p.build), false)
    idecl.is_mut = true
    sub.add_decl(idecl)
    sub.loop_idecl = idecl

    if isset(index_name) {
        sub.set_idf(p, index_name, Idf.for_decl(idecl))
    }

    if !p.loop_first_parse {
        scope.ast.append(Token {
            type: AST.assign
            value1: vgen_decl(idecl)
            value2: vgen_int(0, idecl.type)
        })
    }

    // Fcall
    let values = Array[Value]{ on, vgen_decl(idecl) }
    let fcall = vgen_func_call(p.build, sub, vgen_func_ptr(func), values)

    let retvs = fcall.unroll()
    if retvs.length < 1 : p.error("each_get function must return atleast 1 value")

    // Check error
    let b = p.build
    let break_scope = sub.sub_scope(SCOPE.default)

    break_scope.ast.append(Token {
        type: AST.break
    })
    break_scope.did_return = true

    let breakv = vgen_inline_scope(break_scope, type_none(b))
    fcall = vgen_error_handler(p.ctx, scope, fcall, fcall.trim_errors(), breakv, false)

    if !p.loop_first_parse {
        sub.ast.append(Token {
            type: AST.statement
            value1: fcall
        })
    }

    // Declare values
    each names as name, i {
        let vv = retvs.get(i) ! p.error("Missing 'each_get' return value")
        let vdecl = Decl.new(vv.rett, false)
        vdecl.name = name
        sub.set_idf(p, name, Idf.for_decl(vdecl))
        sub.add_decl(vdecl)

        if !p.loop_first_parse {
            sub.ast.append(Token {
                type: AST.declare
                decl: vdecl
                value1: vv
            })
        }
    }

    // AST
    read_ast(p, sub, single)

    if p.loop_first_parse {
        each sub.decls as decl {
            if decl.is_buffer : continue
            pfast.scope.decls.remove_value(decl)
        }
    }

    return sub
}

fn ast_if(p: Parser, scope: Scope) {
    let cond = read_value(p, scope)
    if !cond.rett.is_bool() : p.error("The if condition value must be of type bool, not: " + cond.rett)
    p.expect2("{", ":", true, true)
    let single = p.word_is(":")
    let sub = scope.sub_scope(SCOPE.if)
    let sub_else = scope.sub_scope(SCOPE.if)
    let has_else = false

    apply_issets(sub, cond.issets)

    read_ast(p, sub, single)

    if sub.did_return : apply_issets(scope, cond.not_issets)
    apply_issets(sub_else, cond.not_issets)

    let t = p.tok(true, true, false)
    if p.word_is("else") {
        has_else = true
        t = p.tok(true, true)
        t = p.tok(true, true, false)
        if p.word_is("if") {
            t = p.tok(true, true)
            ast_if(p, sub_else)
        } else {
            p.expect2("{", ":", true, true)
            let single = p.word_is(":")
            read_ast(p, sub_else, single)
        }
    }

    scope.not_null_remove_check(sub)
    scope.not_null_remove_check(sub_else)
    if has_else {
        apply_issets(scope, sub.not_null.intersect(sub_else.not_null))
    } else {
        let cond_not_issets = cond.not_issets
        if isset(cond_not_issets) : apply_issets(scope, cond_not_issets.intersect(sub.not_null))
    }

    scope.ast.append(Token {
        type: AST.if
        value1: cond
        scope1: sub
        scope2: sub_else
    })
}

fn ast_throw(p: Parser, scope: Scope) {
    let func = p.func()
    let b = p.build

    let err = p.read_word(true, false)
    let hash = func.errors.get(err) ! p.error("Unknown error '" + err + "'. Please add the error to your function declaration.")

    let msg = Str.new_value(err, p.ctx)
    p.tok(true, false, false)
    if p.word_is(",") {
        p.tok(true, false)
        msg = read_value(p, scope)
        if (msg.rett.get_class() !? null) != b.valk_class("type", "String") {
            p.error("Invalid throw message value. Expected a 'String' instead of: " + msg.rett)
        }
    }

    ast_gen_throw(p.ctx, scope, vgen_int(hash, b.error_code_type()), msg, p.clone_chunk().path_and_line())
}

fn ast_cothrow(p: Parser, scope: Scope) {

    let b = p.build
    let err = p.read_word(true, false)
    let hash = helper:ctxhash_u32(err)

    let msg = err
    p.tok(true, false, false)
    if p.word_is(",") {
        p.tok(true, false)
        p.expect_string(true, false)
        msg = p.word()
        msg = msg.part(1, msg.bytes - 2).unescape()
    }

    let tfunc = b.valk_func("coro", "throw")
    let values = Array[Value]{ vgen_int(hash, b.valk_type("type", "uint")), Str.new_value(msg, p.ctx) }
    let fptr = vgen_func_ptr(tfunc)
    let call = vgen_func_call(b, scope, fptr, values)

    scope.ast.append(Token {
        type: AST.statement
        value1: call
    })

    scope.did_return = true
    scope.did_exit = true
}

fn ast_break(p: Parser, scope: Scope) {

    let loop = scope.find_type(SCOPE.loop) ! p.error("Using 'break' without being inside a loop")
    each loop.decls as decl {
        if !decl.type.is_gc() : continue
        scope.ast.append(Token {
            type: AST.assign
            value1: vgen_decl(decl)
            value2: vgen_null(decl.type)
        })
    }

    // Detect when variables can become null at the end of the loop
    let s : ?Scope = scope
    let u = Array[Decl].new()
    while isset(s) {
        let decls = s.not_null_removed
        each decls as decl {
            if u.contains(decl) : continue
            loop.null_at_end.append(decl)
            u.append(decl)
        }
        if s == loop : break
        s = s.parent
    }

    scope.ast.append(Token {
        type: AST.break
    })
    scope.did_return = true
}
fn ast_continue(p: Parser, scope: Scope) {

    let loop = scope.find_type(SCOPE.loop) ! p.error("Using 'break' without being inside a loop")
    each loop.decls as decl {
        if !decl.type.is_gc() : continue
        decl.is_mut = true
        scope.ast.append(Token {
            type: AST.assign
            value1: vgen_decl(decl)
            value2: vgen_null(decl.type)
        })
    }

    // Detect when variables can become null at the start of the loop
    if p.loop_first_parse {
        let s : ?Scope = scope
        let u = Array[Decl].new()
        while isset(s) {
            let decls = s.not_null_removed
            each decls as decl {
                if u.contains(decl) : continue
                loop.null_at_start.append(decl)
                u.append(decl)
            }
            if s == loop : break
            s = s.parent
        }
    }

    let idecl = loop.loop_idecl
    if isset(idecl) {
        scope.ast.append(Token {
            type: AST.assign
            value1: vgen_decl(idecl)
            value2: vgen_op(vgen_decl(idecl), vgen_int(1, idecl.type), OP.add, idecl.type)
        })
    }
    scope.ast.append(Token {
        type: AST.continue
    })
    scope.did_return = true
}

fn ast_ir_comment(p: Parser, scope: Scope) {
    p.expect("(", false, false)

    p.expect_string(true, true)
    let comment = p.word().trim("\"")

    p.expect(")", true, true)

    scope.ast.append(Token {
        type: AST.ir_comment
        string1: comment
    })
}
