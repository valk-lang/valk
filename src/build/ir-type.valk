
fn ir_type(type: Type) String {
    if type.is_pointer() : return "ptr"
    let tt = type.type
    if tt == TYPE.void : return "void"
    if tt == TYPE.error : return "i32"
    if tt == TYPE.int {
        let size = type.size()
        return ir_type_int(size)
    }
    if tt == TYPE.float {
        let size = type.size()
        return ir_type_float(size)
    }
    if tt == TYPE.bool : return "i1"
    if tt == TYPE.array {
        let subtype = type.sub_type
        let arr_size = type.array_size
        if !isset(subtype) : type.build.error("Missing array-type in IR conversion (bug)")
        return "[" + arr_size + " x " + ir_type(subtype) + "]"
    }
    if tt == TYPE.class || tt == TYPE.struct {
        return "[" + type.size() + " x i8]"
    }
    if tt == TYPE.multi {
        let types = type.sub_types ?! type.build.error("Missing types for grouped types object (bug)")
        return ir_type_multi(types)
    }
    type.build.error("Cannot convert type to IR-type: " + type)
}
fn ir_type_multi(types: Array[Type]) String {
    if types.length == 0 : return "void"
    if types.length == 1 : types.get(0) -> first : return ir_type(first)
    let str = "{"
    each types as type, i {
        if i > 0 : str += ", "
        str += ir_type(type)
    }
    str += "}"
    return str
}

fn ir_class_name(class: Class) String {
    return "\%struct." + class.export_name
}

fn ir_type_int(size: uint) String {
    if size == 1 : return "i8"
    if size == 2 : return "i16"
    if size == 4 : return "i32"
    if size == 8 : return "i64"
    return "-unknown-int-size-" + size
}
fn ir_type_float(size: uint) String {
    if size == 4 : return "float"
    if size == 8 : return "double"
    return "-unknown-float-size-" + size
}