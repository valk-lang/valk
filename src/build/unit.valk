
use valk:json
use valk:fs

struct Unit {
    build: Build
    name: String
    functions: Array[Func] (array[Func]{})
    globals: Array[Global] (array[Global]{})
    classes: Array[Class] (array[Class]{})
    consts: Array[Const] (array[Const]{})
    extern_functions: Array[Func] (array[Func]{})
    path_o: String
    path_ir: String
    path_json: String
    _json: ?json:Value (null)
    _ir: ?IR (null)
    changed: bool (true)

    static fn new(b: Build, name: String, hash: String) Unit {
        let base = b.cache_dir + "/" + name + "_" + hash 
        let path_o = base + ".o"
        let path_ir = base + ".ir"
        let path_json = base + ".json"

        let u = Unit {
            build: b
            name: name
            path_o: path_o
            path_ir: path_ir
            path_json: path_json
        }

        b.units.push(u)

        return u
    }

    fn ir() IR {
        let ir = this._ir
        if !isset(ir) {
            let irr = IR.new(this)
            this._ir = irr
            return irr
        }
        return ir
    }

    fn json() ?json:Value {
        let js = this._json
        if isset(js) : return js
        let content = fs:read(this.path_json) ! return null
        js = json:parse(content) ! return null
        this._json = js
        return js
    }

    fn wipe() {
        each this.functions as func {
            func.wipe_ir()
        }
        this.functions.clear()
        this.classes.clear()
        this.globals.clear()
        this.consts.clear()
    }

    fn new_func(fc: Fc, act: int, name: ?String, chunk_args: Chunk) Func !dupe {

        let nsc = fc.nsc
        let n = name ?? ("anon_func_" + this.build.gen_func_name++)

        let f = Func {
            unit: this
            build: this.build
            fc: fc
            export_name: nsc.export_name(n)
            display_name: isset(name) ? nsc.display_name(name) : "<anonymous-function>"
            act: act
            scope: fc.scope.sub_scope(sc_func)
            chunk_args: chunk_args
            from_header: fc.is_header
        }

        f.scope.func = f
        this.functions.push(f)

        return f
    }

    fn new_class(fc: Fc, act: int, name: ?String, type: int, chunk: Chunk) Class !dupe {

        let nsc = fc.nsc
        let n = name ?? ((type == ct_struct ? "anon_struct_" : "anon_class_") + this.build.gen_func_name++)

        let class = Class {
            unit: this
            build: this.build
            fc: fc
            export_name: nsc.export_name(n)
            display_name: isset(name) ? nsc.display_name(name) : (type == ct_struct ? "<anonymous-struct>" : "<anonymous-class>")
            act: act
            type: type
            scope: fc.scope.sub_scope(sc_class)
            chunk: chunk
        }

        this.classes.push(class)

        return class
    }

    fn new_global(fc: Fc, act: int, name: String, chunk_type: Chunk, chunk_value: ?Chunk) Global !dupe {

        let nsc = fc.nsc
        let export_name = nsc.export_name(name)

        let g = Global {
            unit: this
            build: this.build
            fc: fc
            act: act
            export_name: export_name
            display_name: nsc.display_name(name)
            ir_name: "@" + export_name
            chunk_type: chunk_type
            chunk_value: chunk_value
            from_header: fc.is_header
        }

        if !fc.is_header : this.globals.push(g)

        return g
    }
}
