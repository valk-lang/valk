
use valk:json
use valk:fs

struct Unit {
    build: Build
    name: String
    functions: Array[Func] (array[Func]{})
    globals: Array[Global] (array[Global]{})
    classes: Array[Class] (array[Class]{})
    extern_functions: Array[Func] (array[Func]{})
    path_o: String
    path_ir: String
    path_json: String
    _json: ?json:Value (null)
    _ir: ?IR (null)
    changed: bool (true)

    static fn new(b: Build, name: String, hash: String) Unit {
        let base = b.cache_dir + "/" + name + "_" + hash 
        let path_o = base + ".o"
        let path_ir = base + ".ir"
        let path_json = base + ".json"

        let u = Unit {
            build: b
            name: name
            path_o: path_o
            path_ir: path_ir
            path_json: path_json
        }

        b.units.push(u)

        return u
    }

    fn ir() IR {
        let ir = this._ir
        if !isset(ir) {
            let irr = IR.new(this)
            this._ir = irr
            return irr
        }
        return ir
    }

    fn json() ?json:Value {
        let js = this._json
        if isset(js) : return js
        let content = fs:read(this.path_json) ! return null
        js = json:parse(content) ! return null
        this._json = js
        return js
    }

    fn wipe() {
        each this.functions as func {
            func.wipe_ir()
        }
        this.functions.clear()
        this.classes.clear()
        this.globals.clear()
    }

    fn new_func(fc: Fc, act: int, export_name: String, display_name: String, generic_names: ?Array[String], chunk_args: Chunk) Func {

        let nsc = fc.nsc
        // let n = name ?? ("anon_func_" + this.build.gen_func_name++)

        let scope = fc.scope.sub_scope(sc_func)
        let scope_alloca = scope.sub_scope(sc_default)
        let scope_defer = scope.sub_scope(sc_default)

        let f = Func {
            unit: this
            build: this.build
            fc: fc
            export_name: export_name
            display_name: display_name
            act: act
            scope: scope
            scope_alloca: scope_alloca
            scope_defer: scope_defer
            chunk_args: chunk_args
            from_header: fc.is_header
            generic_names: generic_names
            is_generic_base: isset(generic_names)
        }

        f.scope.func = f

        if f.is_generic_base {
            f.generics = map[Func]{}
        } else {
            this.functions.push(f)
            this.build.ast_pipeline.push(f)
        }

        return f
    }

    fn new_class(fc: Fc, act: int, name: String, export_name: String, display_name: String, type: int, generic_names: ?Array[String], chunk: Chunk) Class {

        let nsc = fc.nsc

        let class = Class {
            unit: this
            build: this.build
            fc: fc
            name: name
            export_name: export_name
            display_name: display_name
            act: act
            type: type
            scope: fc.scope.sub_scope(sc_class)
            chunk: chunk
            generic_names: generic_names
            is_generic_base: isset(generic_names)
        }

        if class.is_generic_base {
            class.generics = map[Class]{}
        } else {
            this.classes.push(class)

            if class.type == ct_class {
                this.build.classes.push(class)
            }
        }

        return class
    }

    fn new_global(fc: ?Fc, act: int, name: String, export_name: String, display_name: String, chunk_type: ?Chunk, chunk_value: ?Chunk) Global {

        let g = Global {
            unit: this
            build: this.build
            fc: fc
            act: act
            export_name: export_name
            display_name: display_name
            ir_name: "@" + export_name
            chunk_type: chunk_type
            chunk_value: chunk_value
            from_header: false
        }

        if isset(fc) {
            if fc.is_header {
                g.from_header = false
            } else {
                this.globals.push(g)
            }
        } else {
            this.globals.push(g)
        }

        this.build.globals.push(g)

        return g
    }
}
