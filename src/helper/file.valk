
use valk:fs
use valk:io
use valk:mem

fn file_has_same_content(path: String, content: String) bool {

	let fd = fs:open(path, false, false) ! return false
    let pos : uint = 0
    let len = content.length
    let str_data = content.data_ptr()
    let buf = @stack([10240 x u8])

	while true {
		let readcount = io:read(fd, buf, 10240) ! {
	        io:close(fd)
            return false
        }
        if pos + readcount > len {
	        io:close(fd)
            return false
        }
        if !mem:equal(buf, str_data + pos, readcount) {
	        io:close(fd)
            return false
        }
        pos += readcount
		if readcount != 10240 : break
	}

	io:close(fd)
    return pos == len
}
