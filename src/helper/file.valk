
use valk:fs
use valk:io
use valk:mem
use valk:ext

#if OS == win
extern fn CreateFileA(path: cstring, access: u32, share_mode: u32, lpSecurityAttributes: ?ptr, dwCreationDisposition: u32, dwFlagsAndAttributes: u32, hTemplateFile: ext:HANDLE) ext:HANDLE;
extern fn GetFileTime(file: ext:HANDLE, created_at: ?&<ext:libc_FILETIME>, last_access_at: ?&<ext:libc_FILETIME>, modified_at: ?&<ext:libc_FILETIME>) bool;
#end

@ignore_access

fn file_has_same_content(path: String, content: String) bool {

    let len = content.length
    let flen = fs:size(path)
    if len != flen : return false

	let fd = fs:open(path, false, false) ! return false
    let pos : uint = 0
    let str_data = content.data
    let buf : [u8 x 10240] = @undefined

	while pos < len {
		let readcount = io:read_to_ptr(fd, @stableRef(buf), 10240, pos) ! {
	        io:close(fd)
            return false
        }
        if !mem:equal(buf, str_data + pos, readcount) {
	        io:close(fd)
            return false
        }
        pos += readcount
	}

	io:close(fd)
    return pos == len
}

+ fn modified_time(path: String) uint !file_not_found {
    #if OS == win
    let ftMod : <ext:libc_FILETIME> = @undefined
    let hndl = CreateFileA(path, ext:FILE_READ_ATTRIBUTES, ext:FILE_SHARE_READ | ext:FILE_SHARE_WRITE, null, ext:OPEN_EXISTING, ext:FILE_FLAG_BACKUP_SEMANTICS, 0)
    if hndl == ext:INVALID_HANDLE_VALUE : throw file_not_found
    if !GetFileTime(hndl, null, null, &ftMod) : throw file_not_found
    // FILETIME = 100-ns ticks since 1601-01-01 UTC
    let ticks : u64 = (ftMod.dwHighDateTime.@cast(u64) << 32) | ftMod.dwLowDateTime
    // Subtract difference between 1601-01-01 and 1970-01-01
    let ns100 = ticks - 116444736000000000
    return ns100 * 100 // to nanoseconds
    #else
    let buf = @stack(<ext:libc_stat>)
    fs:stat(path, buf) ! throw file_not_found
    #if OS == linux
    let ns = buf.st_mtim.tv_sec.to(u64) * 1_000_000_000 + buf.st_mtim.tv_nsec
    #elif OS == macos
    let ns = buf.st_mtimespec.tv_sec.to(u64) * 1_000_000_000 + buf.st_mtimespec.tv_nsec
    #end
    return ns
    #end
}
