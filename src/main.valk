
use build
use doc
use valk:gc
use lsp

@ignore_access

fn main(args: Array[String]) i32 {

    if args.contains("--leak-check") {
        args.remove_value("--leak-check")
        gc:verify = true
    }

    args.remove(0)
    let action = args.get(0) ! { help(1) }
    args.remove(0)

    if action == "build" {
        let b = build:init(args) ! build:build_error(EMSG)
        build:run(b)
    } else if action == "doc" {
        let show_help = args.contains("-h") || args.contains("--help")
        if show_help : doc:help(0)
        args.append("--docs")
        let b = build:init(args) ! build:build_error(EMSG)
        build:run(b)
    } else if action == "lsp" {
        lsp:run(args)
    } else if action == "linker-symlinks" {
        link_create_lld_symlinks(build:default_os())
    } else if action.ends_with(".valk") {
        args.prepend("--")
        args.prepend("--run")
        args.prepend(action)
        let b = build:init(args) ! build:build_error(EMSG)
        build:run(b)
    } else if action == "version" {
        println("valk-" + #string(VERSION))
    } else if action == "-h" || action == "--help" {
        help(0)
    } else {
        help(1)
    }

    return 0
}

fn basic_error(msg: String) $exit {
    println("#Error: " + msg)
    exit(1)
}

fn help(code: i32) $exit {
    println("-------------------------")
    println(" Valk v" + #string(VERSION))
    println("-------------------------")
    print("\n")

    println(" Build:        valk build src/*.valk -o myapp");
    println(" Build + Run:  valk build src/*.valk -o myapp --run");
    println(" Run:          valk build src/*.valk --run");
    println(" Run short:    valk main.valk");

    print("\n")
    println(" valk build -h         Build valk code to an executable");
    print("\n")
    println(" valk doc {directory}  Generate docs for a project");
    println(" valk doc -h           Generate docs help");
    println(" valk version          Show compiler version");

    print("\n")

    exit(code)
}
