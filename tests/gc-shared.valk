
@ignore_access

use valk:gc
use valk:thread
use valk:core
use valk:time

class SNode {
    left : ?SNode
    right : ?SNode

    static fn tree(x: int) SNode {
        if x == 0 {
            let res = SNode {}
            return res
        }
        let res = SNode {
            left: SNode.tree(x - 1)
            right: SNode.tree(x - 1)
        }
        return res
    }
}

@shared shared_test_node : ?SNode (null)

fn shared_1() {
    shared_test_node = SNode.tree(12)
}
fn shared_2() {
    let i = 0
    while i++ < 10 {
        shared_test_node = SNode.tree(12)
    }
}

test "GC: Shared Leaks" {
    // Record before memory usage
    let before = gc:mem_shared
    // Create some shared memory
    shared_1()
    // Record new memory usage
    let before2 = gc:mem_shared
    assert(before2 > before)
    // Create much more shared memory
    shared_2()
    // Test shared memory amount
    gc:collect_shared()
    assert(gc:mem_shared == before2)
    // Remove shared memory test
    shared_test_node = null
    gc:collect_shared()
    assert(gc:mem_shared == before)
}

@shared shared_node : ?SNode (null)
@shared shared_lock : core:Mutex[void] (core:Mutex[void].new()) 

fn spam_shared_memory() {
    shared_lock.lock()
    shared_lock.unlock()
    let i = 0
    while i++ < 20 {
        shared_node = SNode.tree(8)
    }
}

test "GC: Shared Leaks Multi Thread" {

    let mem_start = gc:mem_shared

    shared_lock.lock()
    let tt = Array[thread:Thread]{}
    let i = 0
    while i++ < 4 {
        let t = thread:start(fn() {
            spam_shared_memory()
        }) ! panic("Failed to start thread")
        tt.append(t)
    }
    shared_lock.unlock()
    each tt as t {
        t.wait()
    }

    let usage = gc:mem_shared
    assert(usage > mem_start + 10000)
    //
    time:sleep_ms(100)
    //
    gc:collect_shared()
    // Check if atleast half are removed
    assert(gc:mem_shared < usage - 10000)
    usage = gc:mem_shared
    // Clear all
    shared_node = null
    gc:collect_shared()
    // println("Usage: %{usage} -> %{gc:mem_shared} = %{usage - gc:mem_shared}")
    //
    assert(gc:mem_shared < usage - 10000)
}

test "GC: Share null" {
    let a = Array[?String]{null}
    gc:share(a)
}
