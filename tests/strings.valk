
use valk:gc

test "String: concat" {
    assert("test" + " " + "concat" == "test concat")
}

test "String: auto convert integers to string" {
    let n1 : String = -10203
    assert(n1 == "-10203")
    let n2 : String = 10203
    assert(n2 == "10203")
    let n3 : String = (31).to_hex()
    assert(n3 == "1F")
}

test "String: auto convert floats to string" {
    let v : String = -0.129
    assert(v == "-0.13")
    v = -1.40
    assert(v == "-1.40")
    v = -0.0
    assert(v == "0.00")
    v = 1.0
    assert(v == "1.00")
    v = 1.129
    assert(v == "1.13")
    v = 1.40
    assert(v == "1.40")
    v = (1.to(float) / 3)
    assert(v == "0.33")
    v = (1.to(float) / 3).to_str(0)
    assert(v == "0")
    v = (1.to(float) / 3).to_str(3)
    assert(v == "0.333")
    v = (1.to(float) / 3).to_str(4)
    assert(v == "0.3333")
    v = 0.333.to_str(4)
    assert(v == "0.3330")
    v = 0.9999.to_str(3)
    assert(v == "1.000")
    v = 99.9999.to_str(3)
    assert(v == "100.000")
    v = 99.8999.to_str(3)
    assert(v == "99.900")
    v = 99.990.to_str(3)
    assert(v == "99.990")
}

test "String: starts/ends with" {
    assert("abc".starts_with("abc"))
    assert("abc".starts_with("ab"))
    assert("abc".starts_with(""))
    assert("abc".ends_with("abc"))
    assert("abc".ends_with("bc"))
    assert("abc".ends_with(""))
    assert("".ends_with(""))
    // Invalid
    assert(!"abc".starts_with("c"))
    assert(!"abc".starts_with("b"))
    assert(!"abc".ends_with("a"))
    assert(!"abc".ends_with("b"))
    assert(!"".ends_with("e"))
}

test "String: length" {
    assert("".bytes == 0)
    assert("".utf8.length == 0)
    assert("abc".bytes == 3)
    assert("abc".utf8.length == 3)
    assert("ağŸ”c".bytes == 6)
    assert("ağŸ”c".utf8.length == 3)
}

test "String: index of" {
    assert(("ağŸ”c".index_of("") !? 999) == 0)
    assert(("ağŸ”c".utf8.index_of("") !? 999) == 0)
    assert(("ağŸ”c".index_of("c") !? 0) == 5)
    assert(("ağŸ”c".utf8.index_of("c") !? 0) == 2)
    assert(("ağŸ”c".index_of_byte('c') !? 0) == 5)
}

test "String: split" {
    let str = "a=ğŸ”=b"
    let s = str.split("=")
    assert(s.length == 3)
    assert((s.get(0) !? "_") == "a")
    assert((s.get(1) !? "_") == "ğŸ”")
    assert((s.get(2) !? "_") == "b")
    // too long
    s = str.split("valkvalkvalk")
    assert(s.length == 1)
    assert((s.get(0) !? "_") == str)
    // not found
    s = str.split("~")
    assert(s.length == 1)
    assert((s.get(0) !? "_") == str)
    // empty
    s = str.split("")
    assert(s.length == str.length)
    assert((s.get(0) !? "_") == "a")
    // next to eachother
    s = "==a==".split("=")
    assert(s.length == 5)
    assert((s.get(0) !? "_") == "")
    assert((s.get(1) !? "_") == "")
    assert((s.get(2) !? "_") == "a")
    assert((s.get(3) !? "_") == "")
    assert((s.get(4) !? "_") == "")
}

test "String: sub strings" {
    //
    assert("abğŸ”cd".get(0) == 'a')
    assert("abğŸ”cd".get(1) == 'b')
    assert("abğŸ”cd".utf8.get(0) == "a")
    assert("abğŸ”cd".utf8.get(1) == "b")
    assert("abğŸ”cd".utf8.get(2) == "ğŸ”")
    //
    assert("".part(0, 1) == "")
    assert("".part(1, 1) == "")
    assert("abğŸ”cd".utf8.part(1, 1) == "b")
    assert("abğŸ”cd".utf8.part(3, 1) == "c")
    assert("abğŸ”cd".utf8.part(1, 3) == "bğŸ”c")
    assert("abğŸ”cd".utf8.part(1, 10) == "bğŸ”cd")
    //
    assert("".part(0, 1) == "")
    assert("".part(1, 1) == "")
    assert("abğŸ”cd".part(1, 1) == "b")
    assert("abğŸ”cd".part(6, 1) == "c")
    assert("abğŸ”cd".part(1, 6) == "bğŸ”c")
    assert("abğŸ”cd".part(1, 10) == "bğŸ”cd")
    assert("\"abğŸ”\"".utf8.part(1, 3) == "abğŸ”")
}

test "String: comparing" {
    assert("a" != "b")
    assert("a" != "")
    assert("" != "0")
    assert("" < "a")
    assert("a" < "b")
    assert("b" >= "b")
    assert("b" > "a")
    assert("a" <= "a")
    assert("" == "")
}

test "String: #STR" {
    assert(#STR(TEST) == "1")
    assert(#STR(DEF_TEST) == "TestValue")
}

fn str_concat_test() int {
    return 16
}

test "String: %{}" {
    let a = 2
    let b = 5
    assert("a = %a" == "a = 2")
    assert("b = %b" == "b = 5")
    assert("a = %{a}" == "a = 2")
    assert("b = %{b}" == "b = 5")
    assert("a * b = %{a*b}" == "a * b = 10")
    assert("%{ a * str_concat_test() }" == "32")
    assert("%{a}%{b}" == "25")
    assert("%a%b" == "25")
    assert("\%{a}\%{b}" == "%" + "{a}" + "%" + "{b}")
    assert("\%a\%b" == "%" + "a" + "%" + "b")
    assert(r"%{a}%{b}" == "%" + "{a}" + "%" + "{b}")
    assert(r"%a%b" == "%" + "a" + "%" + "b")
}

test "String: compare nullables" {
    let a : ?String = "a" + "b"
    let b : ?String = "a" + "b"
    if false : a = null // Prevent compiler from knowing it's not null
    assert(a == b)
}

test "String: lower/upper" {
    assert("AaZz".lower() == "aazz")
    assert("AaZz".upper() == "AAZZ")
}
