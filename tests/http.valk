
use valk:http
use valk:fs
use valk:json
use valk:net

fn http_handler_data(req: http:Request) http:Response {
    let result = req.method + "|" + req.path
    result += "|" + req.params().get("p1") !? ""
    result += "|" + req.params().get("p2") !? ""
    result += "|" + req.data().get("p3") !? ""
    return http:Response.html(result)
}

fn http_handler_file(req: http:Request) http:Response {
    return http:Response.file(__DIR__ + "/assets/valk-logo.png")
}

fn http_handler(req: http:Request) http:Response {

    let r = http:Router[fn(http:Request)(http:Response)].new()
    r.add("POST", "/test-data", http_handler_data) _
    r.add("GET", "/test-file", http_handler_file) _
    r.add("GET", "/ping", fn() http:Response { return http:Response.text("pong") }) _
    r.add("GET", "/stop", http_handler_file) _

    let route = r.find(req.method, req.path) ! {
        println("ROUTE NOT FOUND: '" + req.path + "'")
        return http:Response.empty(404)
    }
    return route.handler(req)
}

fn http_server() {
    let s = http:Server.new("127.0.0.1", 9000, http_handler) ! return
    s.start(1)
}

test "Http: server/client" {

    co http_server()

    // Request 1
    let post_data = Map[String]{ "p3" => "v3" }
    let json = json:value(post_data)
    // println(json.encode(true))
    let res = http:request("POST", "http://127.0.0.1:9000/test-data?p1=v1", http:Options{
        query_data: Map[String]{ "p2" => "v2" }
        body: json.encode()
        headers: Map[String]{ "content-type" => "application/json" }
    }) ! {
        println("ERR: " + EMSG)
        assert(false)
        return
    }
    assert(res.body == "POST|/test-data|v1|v2|v3")

    // Request 2
    res = http:request("GET", "http://127.0.0.1:9000/test-file?p1=v1") ! {
        println("ERR: " + EMSG)
        assert(false)
        return
    }
    assert(res.body.bytes == 15425)

    // Download to file
    let to = fs:add(fs:exe_dir(), "logo-download.png")
    if fs:exists(to) : fs:delete(to) ! assert(false)
    //
    http:download("http://127.0.0.1:9000/test-file?p1=v1", to) ! {
        println("ERR: " + EMSG)
        assert(false)
    }
    // Check exists
    assert(fs:exists(to))
    // Check file content
    let file_content = fs:read(to) !? <{
        println("Failed reading file")
        println(EMSG)
        return ""
    }
    assert(file_content == res.body)
}

fn non_https_server() {
    let s = http:Server.new("127.0.0.1", 9001, http_handler) ! return
    s.start(1)
}

test "Https: Client" {
    // #if OS == win
    // net:set_ca_cert_path(fs:exe_dir() + "\\cacert.pem")
    // #end

    let req = http:request("GET", "https://valk-lang.dev/api/versions") ! {
        println("REQUEST ERROR (1): " + EMSG)
        assert(false)
        return
    }
    assert(req.body.length > 0)

    // Fail SSL
    co non_https_server()

    let failed = false
    http:request("GET", "https://127.0.0.1:9001/ping") ! {
        // println("REQUEST ERROR (2): " + EMSG)
        failed = true
    }
    assert(failed)

    //
    let res = http:request("GET", "http://127.0.0.1:9001/ping") ! {
        println("REQUEST ERROR (3): " + EMSG)
        assert(false)
        return
    }
    assert(res.body == "pong")
}