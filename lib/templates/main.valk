
@ignore_imut
@ignore_access

use valk:gc
use valk:coro
use valk:core

fn __valk_main__(argc: i32, argv: ptr) i32 $entrance {
    // Init
    gc:thread_init()
    gc:GcLocal.init()
    
    //
    @allocas

    //
    gc:mem_usage = 0
    gc:mem_usage_peak = 0

    // Args
    let args = Array[String].new(8);
    let i = 0
    while i < argc {
        let arg = @ptrv(argv, cstring, i++)
        args.push(arg)
    }

    core:cli_args = args

    // Set global default values
    let gc = gc:local_gc

    #loop globals G T
    G = @global_default_value(G)
    #if is_gc_type(T)
    gc.add_global(@ref(G))
    #end
    #end

    gc.disable = false
    gc.push_to_shared()
    gc:shared_gc.start_loop()

    // Run
    #if TEST
        let main_coro = (co valk_run_all_tests()).@cast(coro:Coro)
    #else
        let main_coro = (co main(args)).@cast(coro:Coro)
    #end

    coro:Coro.loop(main_coro)

    gc:shutdown()
    return 0
}

#if TEST
fn valk_run_all_tests() {
    let results = core:TestResults{}
    let tests = Array[coro:Coro].new(64)

    #loop tests as test_func
    tests.push((co test_func(results)).@cast(coro:Coro))
    #end

    each tests as test {
        coro:await_coro(test)
    }

    results.print()

    if !results.passed() {
        exit(1)
    }
}
#end
