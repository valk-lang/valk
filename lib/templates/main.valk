
@ignore_access

use valk:gc
use valk:core
use valk:coro
use valk:io

fn __valk_main__(argc: i32, argv: ptr) i32 $entrance {
    // Init
    coro:gc_stack_sp = @ref(coro:gc_stack)
    gc:thread_init(true)

    //
    @allocas

    //
    gc:mem_usage_shared = 0
    gc:mem_usage_peak = 0

    #if OS == win
    // Prevent converting \n to \r\n for stdin/stdout
    io:set_mode(0, io:MODE.binary)
    io:set_mode(1, io:MODE.binary)
    #end

    // Args
    let args = Array[String].new(8);
    let i = 0
    while i < argc {
        let arg = @ptrv(argv, cstring, i++)
        args.append(arg)
    }

    core:cli_args = args

    // Store globals & Set default value
    ir_comment("INIT GLOBALS")
    #loop globals G T
    G = @global_default_value(G)
    #if is_gc_type(T)
    gc:gc.globals.add_ptr(@ref(G))
    #end
    #end
    ir_comment("END GLOBALS")

    gc:gc.disable = false

    // Run
    #if TEST
    await co valk_run_all_tests()
    #else
    await co main(args)
    #end

    return 0
}

#if TEST
fn valk_run_all_tests() {
    let results = core:TestResults{}
    let tests = Array[co].new(64)

    // Run sync
    // #loop tests as test_func
    // test_func(results)
    // #end

    // Run async
    #loop tests as test_func
    tests.append(co test_func(results))
    #end

    each tests as test {
        await test
    }
    //

    results.print()

    if !results.passed() {
        exit(1)
    }
}
#end
