
header "sys" as sys

use core
use mem
use gc
use type
use io

global coro_run_first : ?Coro
global coro_run_last : ?Coro
global current_coro : ?Coro
global g_coros : Array[?Coro] (.new(16))
global g_coro_indexes : type:Pool[uint] (.new(16))
global g_coro_count : uint

global main_state : <CoroState> (@undefined)

#if OS == macos
global kqfd : i32
global timer_id : uint
#end

#if OS == win
global cport : ?ptr
#end

struct CoroState {
    rsp: ptr
}
class Coro {
    args: ptr
    gc_args: Array[gc:GcPtr] (.{})

    s: core:Stack (core:Stack.new())

    state: <CoroState> (@undefined)
    stack: ptr (@undefined)

    start_func: fnRef()()
    handler: fnRef()()
    follow_up: ?Coro
    next_resume: ?Coro
    g_list_index: uint (0)

    error_code: u32 (0)
    error_msg: String ("")
    // async results
    poll_event: io:PollEvent (0)
    completion_res: i64 (0)

    test: uint (10)
    error: u32 (0)
    started: bool (false)
    done: bool (false)

    static fn new(handler: ptr, start_func: ptr) SELF {

        let coro = Coro {
            args: mem:alloc(128)
            //
            start_func: start_func.@cast(fnRef()())
            handler: handler.@cast(fnRef()())
        }

        let indexes = g_coro_indexes
        if indexes.count == 0 {
            let index = g_coros.length
            g_coros.append(coro)
            coro.g_list_index = index
        } else {
            let index = indexes.get() !? 0
            g_coros.set(index, coro) _
            coro.g_list_index = index
        }

        let last = coro_run_last
        if isset(last) {
            last.next_resume = coro
            coro_run_last = coro
        } else {
            coro_run_first = coro
            coro_run_last = coro
        }

        #if ARCH == x64
        #if OS == win
        let regs = 30
        #else
        let regs = 6
        #end
        #elif ARCH == arm64
        let regs = 19
        #else
        #error "Unsupported OS: coroutines"
        #end

        let stack = mem:calloc(core:STACK_SIZE)
        let stack_top = stack + core:STACK_SIZE
        let stack_start = stack_top - (regs * size_of(ptr)) - (2 * size_of(ptr))
        stack_start = stack_start - (stack_start.to(uint) & (2 * size_of(ptr) - 1))

        mem:clear(stack_start, regs * size_of(ptr)) // Required for windows

        @ptrv(stack_start, ptr, regs) = start_func
        coro.state.rsp = stack_start
        coro.stack = stack

        g_coro_count++

        return coro
    }

    #if OS == macos
    static fn get_kq() i32 {
        let fd = kqfd
        if fd == 0 {
            kqfd = sys:kqueue()
            if kqfd == -1 : panic("Failed init kqueue")
        }
        return kqfd
    }
    #end

    //////////////
    // AWAIT
    //////////////

    fn await_coro(coro: Coro) {
        if coro.done : return
        let ob = coro
        let ref = ob.follow_up
        while isset(ref) {
            ob = ref
            ref = ref.follow_up
        }
        ob.follow_up = this
        this.block()
    }

    fn sleep(ns: uint) {
        #if OS == linux
        let sqe = io:sqe(this) ! return
        let nsec = ns.@cast(int)
        let ts = io:kernel_timespec {
            tv_sec: nsec / 1000000000
            tv_nsec: (nsec % 1000000000)
        }
        io:io_uring_prep_timeout(sqe, ts, 0, 0)

        #elif OS == macos
        let ms = ns.to(i64) / 1000000
        let ev : <sys:kEvent> = @undefined
        io:EV_SET(@ref(ev), timer_id++, io:EVFILT_TIMER, io:EV_ADD | io:EV_ENABLE | io:EV_ONESHOT, 0, ms, this)
        let kq = this.get_kq()
        sys:kevent(kq, @ref(ev), 1, null, 0, null)

        #else
        let timer = sys:CreateWaitableTimerExW(null, null, 0, sys:TIMER_ALL_ACCESS)
        if timer == 0 : return

        let due : i64 = (ns.to(int) / 100) * -1
        let ok = sys:SetWaitableTimer(timer, &due, 0, null, null, false)
        if !ok {
            sys:CloseHandle(timer)
            return
        }

        let wcp : sys:HANDLE = 0
        let status = sys:NtCreateWaitCompletionPacket(&wcp, sys:MAXIMUM_ALLOWED, null)
        if status != 0 {
            sys:CloseHandle(timer)
            return
        }

        let ov = io:overlap[io:OverlapData]()
        let sig : bool = false
        status = sys:NtAssociateWaitCompletionPacket(wcp, io:iocp(), timer, null, ov, sys:STATUS_SUCCESS, 0, &sig)
        if status != 0 {
            sys:CloseHandle(timer)
            io:free_ov(ov)
            return
        }

        #end
        this.block()

        #if OS == win
        io:free_ov(ov)
        #end
    }

    fn await_last() {
        let last = coro_run_last
        if isset(last) {
            last.next_resume = this
            coro_run_last = this
        } else {
            coro_run_first = this
            coro_run_last = this
        }
        this.block()
    }

    //////////////
    // FLOW
    //////////////

    fn block() {
        sys:valk_stack_swap(@ref(this.state), @ref(main_state))
    }

    fn resume() {
        if this.done : return

        let current = current_coro
        current_coro = this

        core:main_stack.now = core:stack_now
        core:stack_now = this.s.now

        if isset(current) : sys:valk_stack_swap(@ref(current.state), @ref(this.state))
        else : sys:valk_stack_swap(@ref(main_state), @ref(this.state))

        this.s.now = core:stack_now
        core:stack_now = core:main_stack.now
        current_coro = null

        if this.done {
            // Clean up
            this.s.free()
            mem:free(this.stack)
            mem:free(this.args)
            // Next
            let next = this.follow_up
            if isset(next) {
                this.follow_up = null
                next.resume()
            }
        }
    }

    fn complete() {
        this.done = true
        current_coro = null
        g_coro_count--
        let index = this.g_list_index

        g_coros.set(index, null) _
        g_coro_indexes.add(index)

        this.block()
    }

    //////////////
    // LOOP
    //////////////

    static fn loop(until: ?Coro (null)) {
        #if OS == linux
        let ring = io:uring()
        #elif OS == macos
        let kq = Coro.get_kq()
        #elif OS == win
        let cport = io:iocp()
        #end

        let coros = Array[Coro].new(16)

        while true {
            if isset(until) && until.done : break

            // Run coro chain
            let coro = coro_run_first
            while isset(coro) {
                coro.resume()
                let next = coro.next_resume
                coro.next_resume = null
                coro = next
            }
            coro_run_first = null
            coro_run_last = null

            // Done check
            if isset(until) {
                if until.done : break
            }

            // Wait for poll coros
            #if OS == linux
            if io:uring_unsubmitted > 0 : io:uring_submit(ring)
            #end

            gc:collect_if_threshold_almost_reached()
            #if OS == linux
            while true {
                let cqe : ?sys:io_uring_cqe = null
                if coros.length == 0 {
                    gc:unlock()
                    io:io_uring_wait_cqe(ring, @ref(cqe))
                    gc:lock()
                } else {
                    io:io_uring_peek_cqe(ring, @ref(cqe))
                }
                if !isset(cqe) {
                    break
                }
                //
                let coro = cqe.user_data.@cast(Coro)
                coros.append(coro)
                coro.poll_event = cqe.res.@cast(io:PollEvent)
                coro.completion_res = cqe.res
                io:io_uring_cqe_seen(ring, cqe)
            }
            #elif OS == macos
            let events : [<sys:kEvent> x 64] = @undefined
            gc:unlock()
            let nevents = sys:kevent(kq, null, 0, @ref(events), 64, null)
            gc:lock()
            while nevents-- > 0 {
                let ev = events[nevents]
                let coro = ev.udata.@cast(Coro)
                if ev.filter == io:EVFILT_READ {
                    coro.poll_event = sys:POLLIN
                    coro.completion_res = ev.data
                } else if ev.filter == io:EVFILT_WRITE {
                    coro.poll_event = sys:POLLOUT
                    coro.completion_res = ev.data
                }
                coros.append(coro)
            }

            #elif OS == win
            gc:unlock()
            let first = true

            while true {
                let bytes: u32 = 0
                let key: uint = 0
                let ov: ?io:OverlapData = null
                let result = sys:GetQueuedCompletionStatus(cport, &bytes, &key, &ov, first ? sys:INFINITE : 0)
                if !result || !isset(ov) : break
                first = false
                let coro = ov.base.coro.@cast(Coro)
                coros.append(coro)
            }
            gc:lock()

            #else
            panic("Unsupported OS for: coroutine loop")
            #end

            // Resume coroutines
            each coros as coro {
                coro.resume()
            }
            coros.clear()
        }
    }
}
