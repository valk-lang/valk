
use ext
use io

+ enum SOCKET_TYPE {
    TCP
}

+ class Socket {
    ~ host: String
    ~ fd: FD
    ~ port: u16
    - addrinfo: AddrInfo
    - closed: bool (false)

    + static fn server(type: SOCKET_TYPE, host: String, port: u16) SocketServer !invalid_host !create !bind !listen !closed {

        let socket = SELF.create(type, host, port) !>
        socket.bind() !>

        return SocketServer {
            socket: socket
        }
    }

    + static fn client(type: SOCKET_TYPE, host: String, port: u16) Connection !invalid_host !create !connect !closed {
        let socket = SELF.create(type, host, port) !>
        let con = socket.connect() !>
        return con
    }

    static fn create(type: SOCKET_TYPE, host: String, port: u16) Socket !invalid_host !create {
        // 
        #if OS == win
        WSA.init()
        #end

        // Get host info
        let addrinfo = AddrInfo.new(host, port) ! throw invalid_host

        #if OS == win
        let fd = ext:WSASocketA(ext:AF_INET, ext:SOCK_STREAM, ext:IPPROTO_TCP, null, 0, ext:WSA_FLAG_OVERLAPPED)
        if fd == ext:INVALID_SOCKET : throw create
        ext:CreateIoCompletionPort(fd, io:iocp(), 0, 0)

        #else
        let fd = ext:socket(ext:AF_INET, ext:SOCK_STREAM, 0)
        #end

        if fd == -1 : throw create
        #if OS == macos
        io:set_non_block(fd, true)
        #end

        // Set timeout
        #if OS == win
        let timeout : i32 = 1000 * 5 // 5 seconds
        ext:setsockopt(fd, ext:SOL_SOCKET, ext:SO_RCVTIMEO, @ref(timeout), size_of(i32))
        #else
        let timeout = @stack(<ext:libc_timeval>)
        timeout.tv_sec = 5
        timeout.tv_usec = 0
        ext:setsockopt(fd, ext:SOL_SOCKET, ext:SO_RCVTIMEO, timeout, size_of(<ext:libc_timeval>))
        #end

        // SO_NOSIGPIPE
        #if OS == macos
        let nosigv : i32 = 1
        ext:setsockopt(fd, ext:SOL_SOCKET, ext:SO_NOSIGPIPE, @ref(nosigv), size_of(i32))
        #end

        return Socket {
            host: host
            port: port
            fd: fd
            addrinfo: addrinfo
        }
    }

    fn bind() !closed !bind !listen {

        if this.closed : throw closed

        let yes : i32 = 1
        let err : i32 = 0

        ext:setsockopt(this.fd, ext:SOL_SOCKET, ext:SO_REUSEADDR, @ref(yes), size_of(i32))

        err = ext:bind(this.fd, this.addrinfo.sock_addr(), this.addrinfo.addr_len())
        if err != 0 : throw bind

        err = ext:listen(this.fd, 2000000)
        if err != 0 : throw listen
    }

    fn connect() Connection !closed !connect {

        if this.closed : throw closed

        let fd = this.fd
        connect(fd, this.addrinfo) ! {
            this.close()
            throw connect
        }

        this.closed = true // The connection will close the FD
        return Connection.new(fd)
    }

    + fn close() void {
        if this.closed : return
        this.closed = true
        SELF.close_fd(this.fd)
    }

    + static fn close_fd(fd: FD) void {
        #if OS == win
        ext:closesocket(fd.@cast(uint))
        #else
        ext:shutdown(fd, ext:O_RDWR)
        io:close(fd)
        #end
    }
}
