
use utils

struct Response {
    status: u32 (200)
    body: String ("")
    filepath: ?String (null)
    filename: ?String (null)
    content_type: String ("text/plain")
    headers: ?Map[String] (null)

    static fn html(body: String, code: u32 (200), headers: ?Map[String] (null)) Response {
    	return Response{ body: body, status: code, content_type: "text/html", headers: headers }
    }
    static fn json(body: String, code: u32 (200), headers: ?Map[String] (null)) Response {
    	return Response{ body: body, status: code, content_type: "application/json", headers: headers }
    }
    static fn text(body: String, code: u32 (200), content_type: String ("text/plain"), headers: ?Map[String] (null)) Response {
    	return Response{ body: body, status: code, content_type: content_type, headers: headers }
    }
    static fn redirect(location: String, code: u32 (301), headers: ?Map[String] (null)) Response {
    	let h = headers ?? Map[String].new()
    	h.set("Location", location)
    	return Response{ status: code, headers: h }
    }
    static fn empty(code: u32, headers: ?Map[String] (null)) Response {
    	return Response{ status: code, headers: headers }
    }
    static fn file(path: String, filename: ?String (null)) Response {
    	return Response{ filepath: path, filename: filename }
    }

    static fn write_html(req: Request, body: String, code: u32 (200), headers: ?Map[String] (null)) {
        if req.responded : return
        let output = req.output
        if !isset(output) : return
        SELF.compile_real(output, code, "text/html", body, headers)
        req.responded = true
    }

    fn reset() {
        this.status = 200
        this.body = ""
        this.filepath = null
        this.filename = null
        this.content_type = "text/plain"
        let headers = this.headers
        if isset(headers) : headers.clear()
    }

    fn compile(buf: utils:ByteBuffer) {
        SELF.compile_real(buf, this.status, this.content_type, this.body, this.headers)
    }

    static fn compile_real(buf: utils:ByteBuffer, code: u32, content_type: String, body: String, headers: ?Map[String]) void {

        let len = body.bytes

        buf.append_str("HTTP/1.1 ")
        buf.append_uint(code)
        buf.append_str(" OK\r\n")

        buf.append_str("Content-Type: ")
        buf.append_str(content_type)
        buf.append_str("\r\n")

        buf.append_str("Content-Length: ")
        buf.append_uint(body.bytes)
        buf.append_str("\r\n")

        if isset(headers) {
            each headers => k, v {
                buf.append_str(k)
                buf.append_str(": ")
                buf.append_str(v)
                buf.append_str("\r\n")
            }
        }

        buf.append_str("\r\n")
        buf.append_str(body)
    }
}
