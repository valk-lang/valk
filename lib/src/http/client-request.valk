
header "openssl" as ssl

use net
use url
use utils

class ClientResponse {
    + status: uint (0)
    + headers: Map[String] (Map[String].new())
    + body: String ("")
}

class ClientRequest {
    + method: String
    + url: String
    + body: String ("")
    + headers: Map[String] (Map[String].new())

    + fn exec() ClientResponse !invalid_url !connection_failed !invalid_response {

        let parts = url:parse(this.url)

        let is_https = parts.scheme == "https"

        let split = parts.host.split(":")
        let host = parts.host
        let port : u16 = is_https ? 443 : 80
        if split.length > 2 : throw invalid_url
        if split.length == 2 {
            host = split.get(0) !? ""
            let p = split.get(1) ! throw invalid_url
            port = (p.to_uint() ! throw invalid_url) @as u16
        }

        let sock = net:Socket.new_tcp(host, port) ! {
            throw invalid_url, "Invalid request url"
        }

        let con = sock.connect() ! {
            throw connection_failed, "Connection failed"
        }

        if is_https {
            con.ssl_connect() ! {
                con.close()
                throw connection_failed, "SSL connection failed"
            }
        }

        // Send request
        let buf = this.get_send_data(parts)
        con.send_byte_buffer(buf) ! {
            con.close()
            throw connection_failed, "Unexpected non-blocking connection"
        }

        //
        // let parser = Parser.new()
        let input = utils:ByteBuffer.new(2048)
        let context = Context.new(input)
        while true {
            parse_http(input, context, true) ! {
                // Check if we need more data
                if error_is(E, incomplete) {
                    let bytes = con.recv(input, 8192) ! {
                        con.close()
                        throw invalid_response
                    }
                    if bytes > 0 : continue
                }
                con.close()
                throw invalid_response
            }
            if context.done : break
            throw invalid_response
        }

        let resp = ClientResponse {
            status: context.status
            // headers: request.headers()
            body: context.body()
        }

        con.close()
        context.reset()
        input.clear()

        return resp
    }

    - fn get_send_data(parts: url:Url) utils:ByteBuffer {

        let len = this.body.bytes
        let buf = utils:ByteBuffer.new(len + 256)

        let method = this.method

        buf.append_str(method)
        buf.append_str(" ")
        buf.append_str(parts.path)
        if parts.query != "" {
            buf.append_str("?")
            buf.append_str(parts.query)
        }
        buf.append_str(" HTTP/1.1\r\n")

        buf.append_str("Host: ")
        buf.append_str(parts.host)
        buf.append_str("\r\n")

        buf.append_str("Content-Length: ")
        buf.append_str(this.body.bytes)
        buf.append_str("\r\n")

        let headers = this.headers
        each headers._keys => i, k {
            let v = headers._values.get(i) ! break
            buf.append_str(k)
            buf.append_str(": ")
            buf.append_str(v)
            buf.append_str("\r\n")
        }

        buf.append_str("\r\n")
        buf.append_str(this.body)

        return buf
    }
}
