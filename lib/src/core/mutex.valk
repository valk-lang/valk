
header "sys" as sys

use coro
use io

#if OS == win
class MutexWait {
    iocp: FD
    coro: coro:Coro
}
#elif OS == linux
class MutexWait {
    uring: sys:io_uring
    coro: coro:Coro
}
#end

+ class Mutex {
    - locked: bool (false)
    - mutex: SyncMutex[void] (.new())
    - waiters: Array[MutexWait] (.new(128))

    + fn lock() {
        let m = this.mutex
        while true {
            m.lock()
            if this.locked {

                #if OS == linux
                let w = MutexWait {
                    uring: io:uring()
                    coro: coro:current_coro.@cast(coro:Coro)
                }
                #end

                this.waiters.append(w)
                m.unlock()
                coro:yield()
                continue
            }
            this.locked = true
            m.unlock()
            break
        }
    }

    + fn unlock() {
        let m = this.mutex
        m.lock()
        #if OS == linux
        let waiters = this.waiters
        each waiters as w {
            let ring = io:uring()
            let sqe = io:sqe(w.coro) ! panic("Failed to unlock Mutex")
            io:io_uring_prep_msg_ring(sqe, w.uring.ring_fd, w.coro)
            let s = io:uring_submit(ring) // We must submit instantly
        }
        waiters.clear()
        #end
        this.locked = false
        m.unlock()
    }
}
