
@ignore_access

use core

// Core
@shared lock: core:MutexStruct[void] (@undefined)
@shared gc_list: Array[Gc] (@undefined)
// Marks
@shared mark: u8 (2)
// Shared - Counters
shared mem_shared: uint
shared mem_shared_trigger: uint

@shared runner: ?Gc (null)

fn collect_shared() {

    if runner == gc {
        println("Recursion shared gc")
        core:raise(11)
    }

    gc.unlock()
    lock.lock()

    if mark++ > 200 : mark = 2

    let mygc = gc
    runner = mygc
    let gcs = gc_list

    each gcs as gcx {
        gcx.lock()
    }
    each gcs as gcx {
        gc = gcx
        gcx.mark_shared = true
        gcx.collect()
    }
    each gcs as gcx {
        gc = gcx
        gcx.clear_shared()
    }
    gc = mygc
    runner = null

    if shared_trigger_reached : update_shared_trigger()

    each gcs as gcx {
        gcx.unlock()
    }

    lock.unlock()
    gc.lock()
}

fn update_shared_trigger() {
    let high = mem_shared / 100 * 120 + mem_minimum_trigger
    let low = mem_shared_trigger / 100 * 90
    if low > high : high = low
    mem_shared_trigger = high
}
