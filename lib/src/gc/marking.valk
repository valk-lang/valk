
use core

fn increase_block(block: ptr) $inline {
    @ptrv(block, uint, 2)++
    mem_marked += @ptrv(block, uint, 4)
}
fn decrease_block(block: ptr) $inline {
    @ptrv(block, uint, 2)--
    mem_marked -= @ptrv(block, uint, 4)
}

fn increase_block_of_item(item: ptr) {
    let data = (@offset(item, u8, -8)).@cast(GcData)
    let offset = data.block_offset
    if offset > 0 {
        let block = item - offset
        increase_block(block)
    }
}

fn decrease_block_of_item(item: ptr) {
    let data = (@offset(item, u8, -8)).@cast(GcData)
    let offset = data.block_offset
    if offset > 0 {
        let block = item - offset
        decrease_block(block)
    }
}

fn mark_used(item: ?GcItem) $hot {

    let ml = gc.mark_list
    if isset(item) : ml.add(item)

    while true {
        let item = ml.pop() ! break
        let data = gcdata(item)
        // if data.is_still_co_owned() : panic("STILL COO (2)")
        // if data.is_shared() : panic("Setting shared item to owned state")

        if data.state == state_new : increase_block_of_item(item)
        data.state = state_owned

        // Props
        let has_vtable = data.has_vtable()
        let propc = data.propc
        if propc > 0 {
            let prop_base : ptr[?GcItem] = item + size_of(ptr) * has_vtable.@cast(uint)
            // Mark properties
            while propc-- > 0 {
                let sub = prop_base[propc]
                if isset(sub) && (sub & 3 == 0) {
                    let sub_data = gcdata(sub)
                    if sub_data.state < state_owned {
                        // Own
                        @ptrv(prop_base, ?ptr, propc) = sub | 1
                        if sub_data.state == state_new : increase_block_of_item(sub)
                        sub_data.state = state_owned
                        // Mark sub properties
                        if !sub_data.is_shared() : ml.add(sub)
                    } else {
                        // Co-own
                        @ptrv(prop_base, ?ptr, propc) = sub | 2
                        sub_data.co_own()
                    }
                }
            }
        }

        // Call hook gc_action
        if has_vtable {
            let func = @ptrv(data.vtable, ?fnRef(ptr, fnRef(ptr, ptr)())(), 0)
            if isset(func) : func(item, property_mark)
        }
    }
}

fn free_blank(item: ptr) $hot {
    let data = (@offset(item, u8, -8)).@cast(GcData)
    if data.state != state_new : return
    data.remove_has_vtable()
    let vt = data.vtable
    let func = @ptrv(vt, ?fnRef(ptr)(), 1)
    if isset(func) {
        func(item)
    }
}

fn dis_own_rec(item: ptr) {

    let ml = gc.mark_list
    ml.add(item)

    while true {
        let item = (ml.pop() ! break).@cast(GcItem)
        let data = gcdata(item)
        // if data.is_shared() : panic("Dis owning a shared item")
        // if data.mark != 0 : panic("Dis owning an item with a mark")

        decrease_block_of_item(item)

        data.state = state_new
        if data.co_count > 0 {
            gc.re_own_list.add_ptr(item)
        }

        // Props
        let has_vtable = data.has_vtable()
        let propc = data.propc
        if propc > 0 {
            // Mark properties
            let prop_base = item + size_of(ptr) * has_vtable.@cast(uint)
            while propc-- > 0 {
                let sub = @ptrv(prop_base, ?GcItem, propc)
                if isset(sub) {
                    // if sub & 3 == 3 : panic("IS3")
                    if sub & 1 == 1 {
                        sub = sub - 1
                        @ptrv(prop_base, ptr, propc) = sub
                        let data = gcdata(sub)
                        if data.is_shared() {
                            data.state = state_no_owner
                            continue
                        }
                        ml.add(sub)
                    } else if sub & 2 == 2 {
                        sub = sub - 2
                        @ptrv(prop_base, ptr, propc) = sub
                        gcdata(sub).dis_co_own()
                    }
                }
            }
        }

        // Call hook gc_action
        if has_vtable {
            let vt = data.vtable
            let func = @ptrv(vt, ?fnRef(ptr, fnRef(ptr, ptr)())(), 0)
            if isset(func) {
                func(item, property_dis_own)
            }
            if !data.is_in_blanks() {
                data.set_in_blanks()
                gc.blanks.add_ptr(item)
            }
        }
    }
}


fn share_null_check(item: ?ptr) {
    if isset(item) {
        share(item)
    }
}

fn share(item: GcItem) {
    // let data = gcdata(item)
    let ds = gc.dont_stop
    gc.dont_stop = true

    let ml = gc.mark_list
    let start = ml.index
    ml.add(item)

    while true {
        if ml.index == start : break
        let item = (ml.pop() ! break).@cast(GcItem)
        // if item < 0x1000000 : panic("Corrupt share item\n")
        let data = gcdata(item)
        if data.mark == mark {
            // if !data.is_shared() : panic("Has the correct mark, but isnt shared")
            continue
        }

        let is_shared = data.is_shared()
        data.mark = mark

        if !is_shared {
            // if running : panic("Late stage sharing")
            data.set_shared()
            data.remove_still_co_owned()
            let is_new = data.state == state_new
            data.state = state_no_owner

            gc.shared_items.add_ptr(item)

            let offset = data.block_offset
            if offset > 0 {
                let block = item - offset
                let isize = @ptrv(block, uint, 4)
                if is_new {
                    @ptrv(block, uint, 2)++
                } else {
                    // let b = mem_marked
                    mem_marked -= isize
                    // if mem_marked > b : panic("Broken")
                }
                // Increase mem shared
                atomic_op(mem_shared + isize)
            }
        }

        // Props
        let has_vtable = data.has_vtable()
        let propc = data.propc
        if propc > 0 {
            // Mark properties
            let prop_base = item + size_of(ptr) * has_vtable.@cast(uint)
            while propc-- > 0 {
                let sub = @ptrv(prop_base, ?GcItem, propc)
                if isset(sub) {
                    let sub_item : GcItem = sub & -4.to(uint)

                    let sub_data = gcdata(sub_item)
                    if sub & 1 == 1 {
                        // Remove ownership
                        @ptrv(prop_base, ?GcItem, propc) = sub_item
                        sub_data.state = state_no_owner
                    } else if sub & 2 == 2 {
                        // Remove co ownership
                        @ptrv(prop_base, ?GcItem, propc) = sub_item
                        sub_data.dis_co_own()
                    }

                    ml.add(sub_item)
                }
            }
        }

        // Call hook gc_action
        if has_vtable {
            let vt = data.vtable
            let func = @ptrv(vt, ?fnRef(ptr, fnRef(ptr, ptr)())(), 0)
            if isset(func) {
                func(item, property_share)
            }
        }
    }

    gc.dont_stop = ds

    // if !ds : collect_shared_if_threshold_reached()
    // if start == 0 : collect_shared_if_threshold_reached()
}

fn update_mark(item: GcItem) {
    let data = gcdata(item)

    let ml = gc.mark_list
    ml.add(item)

    while true {
        let item = (ml.pop() ! break).@cast(GcItem)
        let data = gcdata(item)
                    // if !data.is_shared() : panic("Marking a non shared item")
        if data.mark == mark : continue 
        data.mark = mark

        // Props
        let has_vtable = data.has_vtable()
        let propc = data.propc
        if propc > 0 {
            // Mark properties
            let prop_base = item + size_of(ptr) * has_vtable.@cast(uint)
            while propc-- > 0 {
                let sub = @ptrv(prop_base, ?GcItem, propc)
                if isset(sub) {
                    let item : GcItem = sub & -4.to(uint)
                    // if !gcdata(item).is_shared() : panic("Shared item has non shared properties")
                    ml.add(item)
                }
            }
        }

        // Call hook gc_action
        if has_vtable {
            let vt = data.vtable
            let func = @ptrv(vt, ?fnRef(ptr, fnRef(ptr, ptr)())(), 0)
            if isset(func) {
                func(item, property_update_mark)
            }
        }
    }
}
