
use core

fn adjust_block(block: ptr, used: bool) {
    if used {
        @ptrv(block, uint, 2)++
        mem_marked += @ptrv(block, uint, 4)
    } else {
        @ptrv(block, uint, 2)--
        mem_marked -= @ptrv(block, uint, 4)
    }
}

fn increase_block(item: ptr) $hot {
    let data = (@offset(item, u8, -8)).@cast(GcData)
    let offset = data.block_offset
    if offset > 0 {
        let block = item - offset
        adjust_block(block, true)
    }
}

fn mark_used(item: ?GcItem) $hot {

    let ml = gc.mark_list
    if isset(item) : ml.add(item)

    while true {
        let item = ml.pop() ! break

        let data = (@offset(item, u8, -8)).@cast(GcData)
        // if data.is_still_co_owned() : panic("STILL COO (2)")
        // if data.is_shared() : panic("Setting shared item to owned state")

        let state = data.state
        data.state = state_owned

        // Increase block counter
        if state == state_new {
            // New item
            let offset = data.block_offset
            if offset > 0 {
                let block = item - offset
                adjust_block(block, true)
            }
        }

        // Props
        let has_vtable = data.has_vtable()
        let propc = data.propc
        if propc > 0 {
            // Mark properties
            let prop_base = item + size_of(ptr) * has_vtable.@cast(uint)
            while propc-- > 0 {
                let sub = @ptrv(prop_base, ?GcItem, propc)
                if isset(sub) && (sub & 3 == 0) {
                    let sub_data = sub.data()
                    if sub_data.state < state_owned && !sub_data.is_shared() {
                        // Own
                        @ptrv(prop_base, ?ptr, propc) = sub | 1
                        if sub_data.state == state_new : increase_block(sub)
                        sub_data.state = state_owned
                        // Mark sub properties
                        ml.add(sub)
                    } else {
                        // Co-own
                        @ptrv(prop_base, ?ptr, propc) = sub | 2
                        sub_data.co_own()
                    }
                }
            }
        }

        // Call hook gc_action
        if has_vtable {
            let vt = data.vtable
            let func = @ptrv(vt, ?fnRef(ptr, fnRef(ptr, ptr)())(), 0)
            if isset(func) {
                func(item, property_mark)
            }
        }
    }
}

fn free_blank(item: ptr) $hot {
    let data = (@offset(item, u8, -8)).@cast(GcData)
    if data.state != state_new : return
    data.remove_has_vtable()
    let vt = data.vtable
    let func = @ptrv(vt, ?fnRef(ptr)(), 1)
    if isset(func) {
        func(item)
    }
}

fn dis_own_rec(item: ptr) {

    let ml = gc.mark_list
    ml.add(item)

    while true {
        let item = (ml.pop() ! break).@cast(GcItem)
        let data = item.data()
        if data.is_shared() : panic("Dis owning a shared item")
        if data.mark != 0 : panic("Dis owning an item with a mark")

        let offset = data.block_offset

        if offset > 0 {
            let block = item - offset
            adjust_block(block, false)
        }

        data.state = state_new
        if data.co_count > 0 {
            gc.re_own_list.add_ptr(item)
        }

        // Props
        let has_vtable = data.has_vtable()
        let propc = data.propc
        if propc > 0 {
            // Mark properties
            let prop_base = item + size_of(ptr) * has_vtable.@cast(uint)
            while propc-- > 0 {
                let sub = @ptrv(prop_base, ?GcItem, propc)
                if isset(sub) {
                    // if sub & 3 == 3 : panic("IS3")
                    if sub & 1 == 1 {
                        sub = sub - 1
                        @ptrv(prop_base, ptr, propc) = sub
                        let data = sub.data()
                        if data.is_shared() {
                            data.state = state_no_owner
                            continue
                        }
                        ml.add(sub)
                    } else if sub & 2 == 2 {
                        sub = sub - 2
                        @ptrv(prop_base, ptr, propc) = sub
                        sub.data().dis_co_own()
                    }
                }
            }
        }

        // Call hook gc_action
        if has_vtable {
            let vt = data.vtable
            let func = @ptrv(vt, ?fnRef(ptr, fnRef(ptr, ptr)())(), 0)
            if isset(func) {
                func(item, property_dis_own)
            }
            if !data.is_in_blanks() {
                data.set_in_blanks()
                gc.blanks.add_ptr(item)
            }
        }
    }
}


fn share_null_check(item: ?ptr) {
    if isset(item) {
        share(item)
    }
}

fn share(item: GcItem) {
    // let data = item.data()
    let ds = gc.dont_stop
    gc.dont_stop = true
        if item < 0x1000000 {
            print("Corrupt INIT ml item\n")
            item.@cast(ptr).print()
            print("\n")
            core:raise(11)
        }

    let ml = gc.mark_list
    let start = ml.index
    ml.add(item)

    while true {
        if ml.index == start : break
        let item = (ml.pop() ! break).@cast(GcItem)
        if item < 0x1000000 {
            print("Corrupt ml item\n")
            ml.index.print(10)
            print("\n")
            core:raise(11)
        }
        // let data = item.data()
        let data = @offset(item, u8, -8).@cast(GcData)
        if data.mark == mark {
            if !data.is_shared() : panic("Has the correct mark, but isnt shared")
            // if !data.is_shared() : panic("Has the correct mark (#{mark}), but isnt shared")
            continue
        }

        let is_shared = data.is_shared()
        data.mark = mark

        if !is_shared {
            if running : panic("Late stage sharing")
            data.set_shared()
            data.remove_still_co_owned()
            let is_new = data.state == state_new
            data.state = state_no_owner

            gc.shared_items.add_ptr(item)

            let offset = data.block_offset
            if offset > 0 {
                let block = item - offset
                let isize = @ptrv(block, uint, 4)
                if is_new {
                    @ptrv(block, uint, 2)++
                } else {
                    let b = mem_marked
                    mem_marked -= isize
                    if mem_marked > b : panic("Broken")
                    // if mem_marked > b : panic("Broken: Before: #{b} | After: #{mem_marked}")
                }
                // Increase mem shared
                atomic_op(mem_shared + isize)
            }
        }

        // Props
        let has_vtable = data.has_vtable()
        let propc = data.propc
        if propc > 0 {
            // Mark properties
            let prop_base = item + size_of(ptr) * has_vtable.@cast(uint)
            while propc-- > 0 {
                let sub = @ptrv(prop_base, ?GcItem, propc)
                if isset(sub) {
        if sub < 0x100000 {
            print("Corrupt shared property (sub)\n")
                        @ptrv(prop_base, ?GcItem, propc) = 123
            core:raise(11)
        }
                    let sub_item : GcItem = sub & -4.to(uint)

                    let sub_data = sub_item.data()
                    if sub & 1 == 1 {
                        // Remove ownership
                        @ptrv(prop_base, ?GcItem, propc) = sub_item
                        sub_data.state = state_no_owner
                    } else if sub & 2 == 2 {
                        // Remove co ownership
                        @ptrv(prop_base, ?GcItem, propc) = sub_item
                        sub_data.dis_co_own()
                    }
        if sub_item < 0x100000 {
            print("Corrupt shared property\n")
            core:raise(11)
        }

                    ml.add(sub_item)
                }
            }
        }

        // Call hook gc_action
        if has_vtable {
            let vt = data.vtable
            let func = @ptrv(vt, ?fnRef(ptr, fnRef(ptr, ptr)())(), 0)
            if isset(func) {
                func(item, property_share)
            }
        }
    }

    gc.dont_stop = ds

    // if !ds : collect_shared_if_threshold_reached()
    // if start == 0 : collect_shared_if_threshold_reached()
}

fn update_mark(item: GcItem) {
    let data = item.data()

    let ml = gc.mark_list
    ml.add(item)

    while true {
        let item = (ml.pop() ! break).@cast(GcItem)
        let data = item.data()
                    if !data.is_shared() : panic("Marking a non shared item")
        if data.mark == mark : continue 
        data.mark = mark

        // Props
        let has_vtable = data.has_vtable()
        let propc = data.propc
        if propc > 0 {
            // Mark properties
            let prop_base = item + size_of(ptr) * has_vtable.@cast(uint)
            while propc-- > 0 {
                let sub = @ptrv(prop_base, ?GcItem, propc)
                if isset(sub) {
                    let item : GcItem = sub & -4.to(uint)
                    if !item.data().is_shared() : panic("Shared item has non shared properties")
                    ml.add(item)
                }
            }
        }

        // Call hook gc_action
        if has_vtable {
            let vt = data.vtable
            let func = @ptrv(vt, ?fnRef(ptr, fnRef(ptr, ptr)())(), 0)
            if isset(func) {
                func(item, property_update_mark)
            }
        }
    }
}
