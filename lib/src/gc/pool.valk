
use mem

global pools: [?Pool x 64]
global alc_list: Bump

value POOL_DATA_OFFSET ((size_of(ptr) * 6.to(uint)))
value POOL_PUBLIC_OFFSET ((size_of(ptr) * 3))

value b_next (0)
value b_slots_total (1)
value b_slots_used_percent (2)
value b_slots_used (3)
value b_props (4)
value b_onfree (5)

fn pools_init() {
    alc_list = Bump.new(64)
    //
    let list : *[?Pool] = @ref(pools)
    let i : uint = size_of(ptr)
    let c : uint = 0
    let until : uint = 16 * size_of(ptr)
    while i <= until {
        let pool = Pool.init(i, null, null)
        list[c++] = pool
        alc_list.add_ptr(pool)
        i += size_of(ptr)
    }
    i = 16 * size_of(ptr) * 2
    while i <= 2147483648 {
        let pool = Pool.init(i, null, null)
        list[c++] = pool
        alc_list.add_ptr(pool)
        i *= 2
    }
}

fn pools_free() {
    let adr = alc_list.data
    let end = adr + alc_list.index
    while adr < end {
        let alc = @ptrv(adr, Pool)
        adr += size_of(ptr)
        alc.free()
    }
    alc_list.free()
}

fn get_allocator(ref: *?Pool, size: uint, props: ?fnptr()(), onfree: ?fnptr()()) Pool {
    let p = ref[0]
    if isset(p) : return p
    p = Pool.init(size, props, onfree)
    alc_list.add_ptr(p)
    return p
}

fn get_pool_index_for_size(size: uint) uint {
    let until : uint = 16 * size_of(ptr)
    if size <= until {
        let index = size / size_of(ptr)
        if index * size_of(ptr) == size : index--
        return index
    }
    let i : uint = 16
    let isize = until * 2
    while isize < size {
        i++
        isize *= 2
    }
    return i
}

fn get_pool_for_size(size: uint) Pool {
    let until : uint = 16 * size_of(ptr)
    if size <= until {
        let index = size / size_of(ptr)
        if index * size_of(ptr) == size : index--
        return get_pool(index, size)
    }
    let i : uint = 16
    let isize = until * 2
    while isize < size {
        i++
        isize *= 2
    }
    return get_pool(i, isize)
}

fn get_pool(i: uint, size: uint) Pool {
    let pool = @ptrv(pools, ?Pool, i)
    if isset(pool) : return pool
    pool = Pool.init(size, null, null)
    @ptrv(pools, Pool, i) = pool
    return pool
}

fn reset_pools(pools: ptr, poolc: uint) {
    let adr = alc_list.data
    let end = adr + alc_list.index
    while adr < end {
        let alc = @ptrv(adr, Pool)
        adr += size_of(ptr)
        alc.reset()
    }
}

fn leak_check_pools(pools: ptr, poolc: uint) {
    let adr = alc_list.data
    let end = adr + alc_list.index
    while adr < end {
        let alc = @ptrv(adr, Pool)
        adr += size_of(ptr)
        alc.leak_check()
    }
}

// fn log_pools() {
//     println("-----------------")
//     let i : uint = 0
//     let last = poolc
//     while i < last {
//         if i > 0 : print(",")
//         let pool = @ptrv(pools, Pool, i++)
//         let x : String = pool.blockc
//         print(x)
//     }
//     print("\n")
// }

struct Pool {
    blockc: uint
    first: ptr
    block: ptr
    item: ptr (null.@cast(.))
    last: ptr (null.@cast(.))
    props: ?ptr
    onfree: ?ptr
    isize: uint (0)

    + static fn init(size: uint, props: ?fnptr()(), onfree: ?fnptr()()) Pool {

        let p = Pool {
            isize: size + 8
            first: null.@cast(.)
            block: null.@cast(.)
            blockc: 1
        }

        return p
    }

    fn free() {
        mem:free(this)
    }

    static fn create_block(size: uint, isize: uint) ptr {
        let max: uint = 65400 / isize
        if max > 240 : max = 240
        if size > max : size = max
        if size == 0 : size = 1
        let alloc_size : uint = size * isize + POOL_DATA_OFFSET
        let b = mem:alloc(alloc_size)
        // Settings
        @ptrv(b, ?ptr, b_next) = null
        @ptrv(b, uint, b_slots_total) = size
        @ptrv(b, uint, b_slots_used_percent) = 0
        @ptrv(b, uint, b_slots_used) = 0
        @ptrv(b, ?ptr, b_props) = null
        @ptrv(b, ?ptr, b_onfree) = null

        // Set offsets
        let data = b + POOL_DATA_OFFSET
        let i : uint = 0
        while i < size {
            let offset = i * isize
            let item : ptr = data + offset
            @ptrv(item, u64) = 0
            let data = item.@cast(GcData)
            data.offset = offset.to(u16) + POOL_PUBLIC_OFFSET
            i++
        }
        // Track memory usage
        gc.mem_usage_thread += alloc_size
        let before = atomic(mem_usage_shared + alloc_size)
        let usage = before + alloc_size
        if usage > mem_usage_peak : mem_usage_peak = usage
        //
        return b
    }

    fn get_vtable() ptr $hot {
        let item = this.get()
        gc.blanks.add_ptr(item)
        return item
    }

    fn get() ptr $hot {

        let isize = this.isize
        if this.block == null {
            let block = Pool.create_block(this.blockc * 2, isize)
            this.first = block
            this.set_block(block)
        }

        let item = this.item
        let last = this.last
        while true {

            if item == last {
                this.set_next_block(this.block)
                item = this.item
                last = this.last
                continue
            }

            let data = item.@cast(GcData)
            if data.is_not_new() {
                item += isize
                continue
            }

            this.item = item + isize

            #if GC_DEBUG
            if data.co_count != 0 : panic("co_count not 0 at alloc")
            if data.flags != 0 {
                print("Flags: ")
                data.flags.print(10)
                print("\n")
                panic("GC error: Flags not 0 on alloc")
            }
            #end

            // Increase mem usage
            gc.mem_new += isize

            return item + 8
        }

        panic("Failed to allocate memory")
    }

    fn set_next_block(block: ptr) {
        while true {
            let next = @ptrv(block, ?ptr, b_next)

            if isset(next) {
                if @ptrv(next, uint, b_slots_used_percent) > 95 {
                    block = next
                    continue
                }

                this.set_block(next)
            } else {
                // Create new block
                let blockc = ++this.blockc
                let new = get_unused_block(this.isize) !? Pool.create_block(blockc * blockc, this.isize)
                // Set next
                @ptrv(block, ptr, b_next) = new
                this.set_block(new)
            }
            break
        }

        // GC check
        collect_if_threshold_reached()
    }

    fn set_block(block: ?ptr) {
        this.block = block.@cast(ptr)
        if isset(block) {
            this.item = block + POOL_DATA_OFFSET
            this.last = block + POOL_DATA_OFFSET + @ptrv(block, uint, b_slots_total) * this.isize
        }
    }

    fn reset() {
        let block = this.first
        if block == null : return

        let current = this.block
        let isize = this.isize

        let reached_current = gc.did_shutdown

        let first_non_empty : ?ptr = null
        let last_non_empty : ?ptr = null
        let first_empty : ?ptr = null
        let last_empty : ?ptr = null

        while block != null {
            let b = block
            let next = @ptrv(b, ptr, b_next)
            let total = @ptrv(b, uint, b_slots_total)
            let used = @ptrv(b, uint, b_slots_used)

            @ptrv(b, ?ptr, b_next) = null // clear next

            // Validate used amount
            #if GC_DEBUG
            let base = b + POOL_DATA_OFFSET
            let end = base + total * isize
            while base < end {
                let data = base.@cast(GcData)
                if data.is_new() {
                    if data.co_count != 0 : panic("GC error: item with state 'new' has a co_count > 0")
                    if data.flags != 0 {
                        print("Flags: ")
                        data.flags.print(10)
                        print("\n")
                        panic("GC error: item with state 'new' contains 'flag' bits")
                    }
                }
                base += isize
            }
            #end

            //
            if used == 0 {
                // Empty blocks
                if reached_current == false {
                    @ptrv(b, uint, b_slots_used_percent) = 0
                    if isset(last_empty) {
                        @ptrv(last_empty, ptr, b_next) = b
                        last_empty = b
                    } else {
                        first_empty = b
                        last_empty = b
                    }
                } else {
                    let alloc_size = total * isize + POOL_DATA_OFFSET
                    gc.mem_usage_thread -= alloc_size
                    atomic(mem_usage_shared - alloc_size)
                    mem:free(b)
                    this.blockc--
                }
            } else {
                // Update usage percent
                @ptrv(b, uint, b_slots_used_percent) = used * 100 / total
                //
                if isset(last_non_empty) {
                    @ptrv(last_non_empty, ptr, b_next) = b
                    last_non_empty = b
                } else {
                    first_non_empty = b
                    last_non_empty = b
                }
            }
            if b == current : reached_current = true
            block = next
        }

        let first : ?ptr = first_non_empty
        if isset(last_non_empty) {
            @ptrv(last_non_empty, ?ptr, b_next) = first_empty
        } else {
            first = first_empty
        }

        this.first = first.@cast(ptr)
        this.set_block(first)
    }

    fn leak_check() {
        let block = this.first
        if block == null : return

        let isize = this.isize

        while block != null {
            let b = block
            let next = @ptrv(b, ptr, b_next)
            let total = @ptrv(b, uint, b_slots_total)
            // let used = @ptrv(b, uint, b_slots_used)

            let base = b + POOL_DATA_OFFSET
            let end = base + total * isize
            while base < end {
                let data = base.@cast(GcData)
                if data.is_new() {
                    // let mark = data.block_offset & 3
                    // if mark != 0 : panic("GC error: item with state 'new' contains 'mark' bits")
                    if data.co_count != 0 : panic("GC error: item with state 'new' has a co_count > 0 (leak-check)")
                    if data.flags != 0 {
                        print("Flags: ")
                        data.flags.print(10)
                        print("\n")
                        panic("GC error: item with state 'new' contains 'flag' bits")
                    }
                } else if !data.has_leak_mark() && !data.is_shared() {
                    print_item_info(data.item())
                    panic("Memory leak")
                }
                base += isize
            }

            block = next
        }
    }
}
