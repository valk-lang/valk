
use core
use mem

global poolc: uint
global pools: <[128 x ptr]>

value POOL_DATA_OFFSET ((size_of(ptr) * 5.to(uint)))

value in_list_disconnected (1)
value in_list_reconnect (2)
value in_list_blanks (4)

fn pools_init() {
    let list = pools

    let i : uint = size_of(ptr)
    let c : uint = 0
    while i <= 16 * size_of(ptr) {
        let pool = Pool.init(i)
        @ptrv(list, Pool, c) = pool
        i += size_of(ptr)
        c++
    }
    i = 16 * size_of(ptr) * 2
    while i <= 4096 {
        let pool = Pool.init(i)
        @ptrv(list, Pool, c) = pool
        i *= 2
        c++
    }

    poolc = c
}

fn reset_pools() {
    let i : uint = 0
    let last = poolc
    while i < last {
        let pool = @ptrv(pools, Pool, i++)
        pool.reset()
    }
}

fn log_pools() {
    println("-----------------")
    let i : uint = 0
    let last = poolc
    while i < last {
        if i > 0 : print(",")
        let pool = @ptrv(pools, Pool, i++)
        let x : String = pool.blockc
        print(x)
    }
    print("\n")
}

struct Pool {
    blockc: uint
    first: ptr
    block: ptr
    index: uint (0)
    isize: uint (0)

    + static fn init(size: uint) Pool {

        let p = Pool {
            isize: size + 8
            first: null @as ptr
            block: null @as ptr
            blockc: 1
        }

        return p
    }

    static fn create_block(size: uint, isize: uint) ptr {
        let max: uint = 65400 / isize
        if max > 248 : max = 248
        if size > max : size = max
        let b = mem:alloc(size * isize + POOL_DATA_OFFSET)
        // Settings
        @ptrv(b, ?ptr, 0) = null // next block
        @ptrv(b, uint, 1) = size // count
        @ptrv(b, uint, 2) = 0 // items used
        @ptrv(b, uint, 3) = 0 // usage percent
        @ptrv(b, uint, 4) = isize // item size
        // Set indexes
        let data = b + POOL_DATA_OFFSET
        let base_offset = POOL_DATA_OFFSET + 8
        let i : uint = 0
        while i < size {
            let offset = i * isize
            let item : ptr = data + offset
            @ptrv(item, u64) = 0
            @ptrv(item, u16, 3) = (offset + base_offset) @as u16
            i++
        }
        return b
    }

    fn get_vtable() ptr $hot {
        let item = this.get()
        let data = (@offset(item, u8, -8)) @as GcData
        data.has_vtable = 1
        @ptrv(blanks.get(size_of(ptr)), ptr) = item
        return item
    }

    fn get() ptr $hot {

        let isize = this.isize
        if this.block == null {
            this.block = Pool.create_block(this.blockc * 2, isize)
            this.first = this.block
        }

        while true {
            let block = this.block
            let index = this.index
            if index == @ptrv(block, uint, 1) {
                this.set_next_block(block)
                continue
            }
            this.index++
            let block_data = block + POOL_DATA_OFFSET
            let item = block_data + index * isize
            let data = item @as GcData

            if data.state != state_new : continue

            // Increase mem usage
            mem_new += isize

            return item + 8
        }

        panic("Failed to allocate memory")
    }

    fn set_next_block(block: ptr) {
        while true {
            let next = @ptrv(block, ?ptr, 0)

            if isset(next) {
                if @ptrv(next, uint, 3) > 90 {
                    block = next
                    continue
                }

                this.set_block(next)
            } else {
                // Create new block
                let blockc = ++this.blockc
                let new = Pool.create_block(blockc * blockc, this.isize)
                // Set next
                @ptrv(block, ptr, 0) = new
                this.set_block(new)
            }
            break
        }

        // GC check
        run_if_threshold_reached()
    }

    fn set_block(block: ptr) {
        this.block = block
        this.index = 0
    }

    fn reset() {
        let block = this.first
        if block == null : return

        let current = this.block
        let isize = this.isize

        let reached_current = false
        let last : ?ptr = null
        let first_empty_block : ?ptr = null
        let last_empty_block : ?ptr = null

        while block != null {
            let b = block
            let next = @ptrv(b, ptr, 0)
            let total = @ptrv(b, uint, 1)
            let used = @ptrv(b, uint, 2)

            @ptrv(b, ?ptr, 0) = null // clear next

            //
            if used == 0 {
                // Empty blocks
                if reached_current == false {
                    @ptrv(b, uint, 3) = 0 // Set percent used
                    if isset(last_empty_block) {
                        @ptrv(last_empty_block, ptr, 0) = b
                        last_empty_block = b
                    } else {
                        first_empty_block = b
                        last_empty_block = b
                    }
                } else {
                    mem:free(b)
                    this.blockc--
                }
            } else {
                // Update usage percent
                @ptrv(b, uint, 3) = used * 100 / total
                //
                if isset(last) : @ptrv(last, ptr, 0) = b
                else : this.first = b
                last = b
            }
            if b == current : reached_current = true
            block = next
        }

        if isset(first_empty_block) {
            if isset(last)  {
                @ptrv(last, ptr, 0) = first_empty_block
            } else {
                this.first = first_empty_block
            }
        } else {
            if isset(last)  {
                @ptrv(last, ?ptr, 0) = null
            }
        }

        this.set_block(this.first)
    }
}
