
use coro

global local_gc : GcLocal (@undefined)

global stack_main : Stack (@undefined)
global stack_active : Stack (@undefined)
global stack_items : Bump (@undefined)
global disable_colors : Bump (@undefined)
global return_colors : Bump (@undefined)
global still_used : Bump (@undefined)
global blanks : Bump (@undefined)

global mem_new : uint
global mem_trigger : uint
global mem_suggestion : uint
global mem_marked : uint

value mem_minimum_trigger (8192)

value FROZEN (255)

+ fn run_now() {
    local_gc.run()
}

+ fn run_if_threshold_reached() {
    if mem_new > mem_trigger || local_gc.update_shared {
        local_gc.run()
    }
}

+ fn run_if_threshold_almost_reached() {
    if mem_new > mem_suggestion || local_gc.update_shared {
        local_gc.run()
    }
}
+ fn shutdown() {
    local_gc.shutdown()
}

fn thread_init() {
    stack_main = Stack.new()
    stack_active = stack_main
    pools_init()
}

struct GcLocal {
    globals: Bump (Bump.new())
    update_shared: bool (false)
    shared_mark_applied: u8 (0)
    disable: bool (false)

    + static fn init() GcLocal {

        stack_items = Bump.new()
        disable_colors = Bump.new()
        return_colors = Bump.new()
        still_used = Bump.new()
        if blanks == null @as Bump : blanks = Bump.new()

        Color.init()

        mem_new = 0
        mem_trigger = mem_minimum_trigger
        mem_suggestion = mem_minimum_trigger

        let gcl = GcLocal {
        }
        local_gc = gcl

        return gcl
    }

    fn add_global(ref: ptr) {
        let adr = this.globals.get(sizeof(ptr))
        @ptrv(adr, ptr) = ref
    }

    fn push_to_shared() {
        shared_gc.add_gc(this)
    }

    fn run() {

        if this.disable : return
        this.disable = true

        // this.lock.lock()
        // print("-")
        // print("<<<<<<<<<<<<<<<<<<<<<<<<<<\n")

        let s_mark = shared_mark

        // Make sure share color is valid
        @ptrv(color_state, u8, color_shared) = color_state_used

        // Check if items have dissapeared from the stack
        this.check_previous_stack_items()

        // Mark objects that are still used
        this.mark_stacks()

        // Check if objects are still used
        this.check_still_used()
        still_used.reset()
        this.check_blanks()
        blanks.reset()

        // Reset pools
        reset_pools()

        // Disable colors that had conflicts
        this.check_disable_colors()
        disable_colors.reset()
        this.check_return_colors()
        return_colors.reset()

        // Update gc trigger point
        mem_trigger = mem_marked / 10 * 12 + mem_minimum_trigger
        mem_suggestion = mem_marked / 10 * 11 + mem_minimum_trigger
        mem_new = mem_marked

        // Mark shared
        if this.update_shared && s_mark == shared_mark {
            this.update_shared = false
            this.shared_mark_applied = s_mark
        }

        this.disable = false
        // print(">>>>>>>>>>>>>>>>>>\n")
        // this.lock.unlock()
        // print(".")
    }

    fn check_previous_stack_items() {

        this.mark_on_stack(1)

        let stu = still_used
        let state = color_state

        let list = stack_items
        let data = list.data
        let adr = data
        let end = adr + list.index
        list.index = 0 // Clear list
        while adr < end {
            let item = @ptrv(adr, ptr)
            let color = @ptrv(adr, uint, 1) @as color_type
            adr += sizeof(ptr) * 2

            let mark = @ptrv(item, u8, byte_mark_rel)

            // Check on-stack mark
            if mark & 1 == 0 {
                // Disable color
                if color >= color_custom {
                    @ptrv(state, u8, color) = color_state_unused
                    let adr = return_colors.get(sizeof(color_type))
                    @ptrv(adr, color_type) = color

                    // Check in-still-used-list mark
                    if mark & 2 == 0 {
                        @ptrv(item, u8, byte_mark_rel) = mark | 2
                        let adr = stu.get(sizeof(ptr))
                        @ptrv(adr, ptr) = item
                    }
                }
                
            } else {
                // Re-add to list
                if @ptrv(state, u8, color) == color_state_used {
                    @ptrv(data, ptr) = item
                    @ptrv(data, uint, 1) = color
                    data += sizeof(ptr) * 2
                } else {
                    let adr = return_colors.get(sizeof(color_type))
                    @ptrv(adr, color_type) = color
                }
            }
        }
        list.index = data - list.data

        this.mark_on_stack(0)
    }

    fn mark_on_stack(val: u8) {

        // Globals
        let g_adr = this.globals.data
        let g_end_adr = g_adr + this.globals.index
        while g_adr < g_end_adr {
            let ref = @ptrv(g_adr, ptr)
            let item = @ptrv(ref, ?ptr)
            if isset(item) {
                if val == 1 {
                    @ptrv(item, u8, byte_mark_rel) = @ptrv(item, u8, byte_mark_rel) | 1
                } else {
                    @ptrv(item, u8, byte_mark_rel) = @ptrv(item, u8, byte_mark_rel) & 254
                }
            }
            g_adr += sizeof(ptr)
        }

        // Stack
        let stack = stack_main
        let adr = stack.base
        let top = stack.adr
        while adr < top {
            let item = @ptrv(adr, ?ptr)
            if isset(item) {
                if val == 1 {
                    @ptrv(item, u8, byte_mark_rel) = @ptrv(item, u8, byte_mark_rel) | 1
                } else {
                    @ptrv(item, u8, byte_mark_rel) = @ptrv(item, u8, byte_mark_rel) & 254
                }
            }
            adr += sizeof(ptr)
        }

        let coro_data = coro:g_coros.data
        let coro_count = coro:g_coros.length
        while coro_count-- > 0 {
            let coro = @ptrv(coro_data, ?ptr, coro_count)
            if isset(coro) {
                let adr = (coro @as coro:Coro).stack.base
                let top = (coro @as coro:Coro).stack.adr
                while adr < top {
                    let item = @ptrv(adr, ?ptr)
                    if isset(item) {
                        if val == 1 {
                            @ptrv(item, u8, byte_mark_rel) = @ptrv(item, u8, byte_mark_rel) | 1
                        } else {
                            @ptrv(item, u8, byte_mark_rel) = @ptrv(item, u8, byte_mark_rel) & 254
                        }
                    }
                    adr += sizeof(ptr)
                }
            }
        }
    }

    fn mark_stacks() {

        // Globals
        let g_adr = this.globals.data
        let g_end_adr = g_adr + this.globals.index
        while g_adr < g_end_adr {
            let ref = @ptrv(g_adr, ptr)
            let item = @ptrv(ref, ptr)
            if item != null {
                this.mark_item(item)
            }
            g_adr += sizeof(ptr)
        }

        // Stack
        let stack = stack_main
        let adr = stack.base
        let top = stack.adr
        while adr < top {
            let item = @ptrv(adr, ptr, 0)
            if item != null {
                this.mark_item(item)
            }
            adr += sizeof(ptr)
        }

        let coro_data = coro:g_coros.data
        let coro_count = coro:g_coros.length
        while coro_count-- > 0 {
            let coro = @ptrv(coro_data, ?ptr, coro_count)
            if isset(coro) {
                let adr = (coro @as coro:Coro).stack.base
                let top = (coro @as coro:Coro).stack.adr
                while adr < top {
                    let item = @ptrv(adr, ptr)
                    if item != null {
                        this.mark_item(item)
                    }
                    adr += sizeof(ptr)
                }
            }
        }
    }

    fn mark_item(item: ptr) {
        let color = @ptrv(item, color_type, byte_color_rel)
        if @ptrv(color_state, u8, color) == color_state_unused {
            let c = Color.get()
            color_disable_after = false

            // mark(item, c)
            let vti = @ptrv(item, u16, byte_vtable_index_rel)
            let func = @ptrv(gc_tables, fn(ptr, color_type)(), vti * GC_TABLE_SIZE + vt_mark)
            func(item, c)

            if color_disable_after {
                let adr = disable_colors.get(sizeof(color_type))
                @ptrv(adr, color_type) = c
            }

            let li = stack_items.get(sizeof(ptr) * 2)
            @ptrv(li, ptr) = item
            @ptrv(li, uint, 1) = c
        }
    }

    fn check_still_used() {

        let state = color_state
        let list = still_used
        let data = list.data
        let adr = data
        let end = adr + list.index

        while adr < end {
            let item = @ptrv(adr, ptr)
            adr += sizeof(ptr)

            // Remove mark
            let mark = @ptrv(item, u8, byte_mark_rel)
            @ptrv(item, u8, byte_mark_rel) = mark & 253

            // Check mark
            let color = @ptrv(item, color_type, byte_color_rel)
            if @ptrv(state, u8, color) == color_state_unused {
                // Free item
                // free(item)
                let vti = @ptrv(item, u16, byte_vtable_index_rel)
                let func = @ptrv(gc_tables, fn(ptr)(), vti * GC_TABLE_SIZE + vt_free)
                func(item)

                continue
            } else {
                // TODO: move 'remove-mark' code here
            }
        }
    }

    fn check_blanks() {

        let list = blanks
        let data = list.data
        let adr = data
        let end = adr + list.index

        while adr < end {
            let item = @ptrv(adr, ptr)
            adr += sizeof(ptr)
            free_blank(item)
        }
    }

    fn check_disable_colors() {
        let list = disable_colors
        let state = color_state

        let data = list.data
        let adr = data
        let end = adr + list.index
        while adr < end {
            let color = @ptrv(adr, color_type)
            adr += sizeof(color_type)
            @ptrv(state, u8, color) = color_state_unused
        }
    }

    fn check_return_colors() {
        let list = return_colors
        let state = color_state

        let data = list.data
        let adr = data
        let end = adr + list.index
        while adr < end {
            let color = @ptrv(adr, color_type)
            adr += sizeof(color_type)
            Color.give(color)
        }
    }

    + fn shutdown() {
        this.run()
        // TODO: pass blocks with shared memory to shared gc
    }
}
