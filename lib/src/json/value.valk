
class Value {
    type: int
    object_values: ?Map[Value] (null)
    array_values: ?Array[Value] (null)
    int_value: int (0)
    float_value: float (0.0)
    string_value: String ("")
    bool_value: bool (false)

    + fn get(key: String) Value {
        this.type = type_object
        let vv = this.object_values
        if isset(vv) : vv.get(key) -> val : return val
        let val = Value{ type: type_null }
        this.set(key, val)
        return val
    }
    + fn set(key: String, val: Value) {
        this.type = type_object
        let vv = this.object_values
        if !isset(vv) {
            vv = map[Value]{}
            this.object_values = vv
        }
        vv.set(key, val)
    }
    + fn has(key: String) bool {
        let vv = this.object_values
        if isset(vv) : return vv.has(key)
        return false
    }
    + fn remove(key: String) {
        let vv = this.object_values
        if isset(vv) : vv.remove(key)
    }
    //
    + fn add(value: Value) {
        this.type = type_array
        let vv = this.array_values
        if !isset(vv) {
            vv = array[Value]{}
            this.array_values = vv
        }
        vv.append(value)
    }

    //
    + fn string() String $to {
        if this.is_string() : return this.string_value
        return ""
    }
    + fn int() int $to {
        if this.is_number() : return this.int_value
        return 0
    }
    + fn float() float $to {
        if this.is_number() : return this.float_value
        return 0
    }
    + fn bool() bool $to {
        if this.is_bool() : return this.bool_value
        return false
    }
    + fn array() Array[Value] $to {
        if this.is_array() {
            let a = this.array_values
            if isset(a) : return a
        }
        return array[Value]{}
    }
    + fn map() Map[Value] $to {
        if this.is_object() {
            let a = this.object_values
            if isset(a) : return a
        }
        return map[Value]{}
    }

    + fn is_null() bool {
        return this.type == type_null
    }
    + fn is_string() bool {
        return this.type == type_string
    }
    + fn is_bool() bool {
        return this.type == type_bool
    }
    + fn is_number() bool {
        return this.type == type_number
    }
    + fn is_array() bool {
        return this.type == type_array
    }
    + fn is_object() bool {
        return this.type == type_object
    }

    // Quick object properties
    + fn set_null(key: String) SELF {
        this.set(key, new_null())
        return this
    }
    + fn set_string(key: String, value: String) SELF {
        this.set(key, new_string(value))
        return this
    }
    + fn set_number_int(key: String, value: int) SELF {
        this.set(key, new_int(value))
        return this
    }
    + fn set_number_uint(key: String, value: uint) SELF {
        this.set(key, new_int(value.to(int)))
        return this
    }
    + fn set_number_float(key: String, value: float) SELF {
        this.set(key, new_float(value))
        return this
    }
    + fn set_bool(key: String, value: bool) SELF {
        this.set(key, new_bool(value))
        return this
    }
    + fn set_array(key: String, values: Array[Value]) SELF {
        this.set(key, new_array(values))
        return this
    }
    + fn set_object(key: String, values: Map[Value]) SELF {
        this.set(key, new_object(values))
        return this
    }

    // Quick array items
    + fn add_null() SELF {
        this.add(new_null())
        return this
    }
    + fn add_string(value: String) SELF {
        this.add(new_string(value))
        return this
    }
    + fn add_number_int(value: int) SELF {
        this.add(new_int(value))
        return this
    }
    + fn add_number_uint(value: uint) SELF {
        this.add(new_int(value.to(int)))
        return this
    }
    + fn add_number_float(value: float) SELF {
        this.add(new_float(value))
        return this
    }
    + fn add_bool(value: bool) SELF {
        this.add(new_bool(value))
        return this
    }
    + fn add_array(value: Array[Value]) SELF {
        this.add(new_array(value))
        return this
    }
    + fn add_object(value: Map[Value]) SELF {
        this.add(new_object(value))
        return this
    }


    //
    + fn encode(pretty: bool (false)) String {
        return encode_value(this, pretty)
    }
}
