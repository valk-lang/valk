
header "sys" as sys

use mem
use io
use utils

+ fn open(path: String, writable: bool, append_on_write: bool) FD !open {
    return open_extend(path, writable, append_on_write, false) ! throw open
}
// TODO: use octal number for permissions
+ fn open_extend(path: String, writable: bool, append_on_write: bool, create_file_if_doesnt_exist: bool (false), create_file_permissions: u32 (420)) FD !open !access {

    // let flags : i32 = (sys:O_RDONLY | sys:O_NONBLOCK) @as i32
    let flags : i32 = sys:O_RDONLY @as i32
    #if OS == win
    flags = flags | 0x8000 // O_BINARY
    #end

    if writable : flags = sys:O_RDWR
    if create_file_if_doesnt_exist : flags = flags | sys:O_CREAT
    if writable {
        flags = flags | (append_on_write ? sys:O_APPEND : sys:O_TRUNC)
    }

    let fd = sys:open(path.data_cstring(), flags, create_file_permissions)
    if fd < 0 {
        // let err = core:libc_errno()
        // if err == 13 {
        //     throw access
        // }
        throw open
    }

    return fd @as FD
}

- fn stat(path: String, buf: sys:libc_stat) !failed {
    let res = sys:stat(path.data_cstring(), buf)
    if res == -1 : throw failed
}
+ fn size(path: String) uint {
    let buf = @stack(<sys:libc_stat>)
    stat(path, buf) ! return 0
    return buf.st_size @as uint
}

+ fn sync() {
    sys:sync()
}

// TODO: !open instead of !open_err (err identifiers need to be split)
+ fn read(path: String) String !open_err !read !close {

	let fd = open(path, false, false) ! throw open_err

	let res = utils:ByteBuffer.new(4096)
	while true {
        res.minimum_free_space(4096)
		let readcount = io:read(fd, res.data + res.length, 4096) ! throw read
        res.length += readcount
		if(readcount != 4096) {
			break
		}
	}

	let check = io:close(fd)
	// if check != 0 {
	// 	throw close
	// }

	return res.to_string()
}

// TODO: !open instead of !open_err (err identifiers need to be split)
+ fn write(path: String, content: String, append: bool (false)) !open_err {

    let fd = open_extend(path, true, append, true) ! throw open_err

    io:write(fd, content) _
    io:close(fd)
}

+ fn write_from_ptr(path: String, data: ptr, size: uint, append: bool (false)) !open_err {

    let fd = open_extend(path, true, append, true) ! throw open_err

    io:write_from_ptr(fd, data, size) ? 0
    io:close(fd)
}

+ fn exists(path: String) bool {
	#if OS == win
	let dwAttrib = sys:GetFileAttributesA(path.data_cstring())
    return dwAttrib != sys:INVALID_FILE_ATTRIBUTES_fix
	#else
    let buf = @stack(<sys:libc_stat>)
    stat(path, buf) ! return false
    return true
    #end
}
+ fn remove(path: String) !failed {
    let res = sys:unlink(path.data_cstring())
    if res == -1 {
        throw failed
    }
}

// TODO: use octal numbers
// 493 = 0755
+ fn mkdir(path: String, permissions: u32 (493)) !failed {
    if sys:mkdir(path.data_cstring(), permissions) == -1 {
        throw failed
    }
}
+ fn rmdir(path: String) !failed {
    if sys:rmdir(path.data_cstring()) == -1 {
        throw failed
    }
}

+ fn is_file(path: String) bool {
    let buf = @stack(<sys:libc_stat>)
    stat(path, buf) ! return false
    return (buf.st_mode & sys:S_IFMT) == sys:S_IFREG
}
+ fn is_dir(path: String) bool {
    let buf = @stack(<sys:libc_stat>)
    stat(path, buf) ! return false
    return (buf.st_mode & sys:S_IFDIR) == sys:S_IFDIR
}

+ fn files_in(directory: String, files: bool (true), dirs: bool (true)) Array[String] {

    let result = Array[String].new()

    #if OS == win

    let path = directory.rtrim("\\").rtrim("//") + "\\"
    let pat = path + "*"
    let pattern = pat.data_cstring()
    let data = @stack(<sys:libc_WIN32_FIND_DATAA>)
    let hFind = sys:FindFirstFileA(pattern, data)
    if hFind != -1 @as ptr {
        let loop = true
        while loop {
            let fn = (@ref(data.cFileName) @as cstring).to_string()

            if fn.bytes < 3 && (fn == "." || fn == "..") {
                loop = sys:FindNextFileA(hFind, data)
                continue
            }

            // TODO: use 0x10, instead of 16
            let is_dir = (data.dwFileAttributes & 16) > 0
            let is_file = !is_dir

            if (dirs && is_dir) || (files && is_file) {
                result.push(path + fn)
            }

            loop = sys:FindNextFileA(hFind, data)
        }
        sys:FindClose(hFind)
    }

    #else

    let dir = directory.rtrim("/") + "/"
    let d = sys:opendir(dir.data_cstring())

    if isset(d) {
        while true {
            let ent = sys:readdir(d)
            if isset(ent) {
                let fn = (@ref(ent.d_name) @as cstring).to_string()

                if fn.bytes < 3 && (fn == "." || fn == ".." || fn == "") {
                    continue
                }

                let fpath = dir + fn
                let _is_dir = is_dir(fpath)
                let _is_file = !_is_dir

                if (dirs && _is_dir) || (files && _is_file) {
                    result.push(fpath)
                }
                continue
            }
            break

        }
        sys:closedir(d)
    }

    #end

    return result
}
