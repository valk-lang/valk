
header "sys" as sys

use mem
use core

- global EXE_PATH : ?String (null)
- global EXE_DIR : ?String (null)

+ value PATH_MAX (4096)

#if OS == win
+ value PATH_DIV ("\\")
+ value PATH_DIV_TWICE ("\\\\")
+ value PATH_DIV_REPLACE ("/")
#else
+ value PATH_DIV ("/")
+ value PATH_DIV_TWICE ("//")
+ value PATH_DIV_REPLACE ("\\")
#end

+ fn path(path: String) Path {
    return Path.new(path)
}

class Path imut {
    - path: String

    + static fn new(path: String) SELF {
        path = realpath(path)
        path = path.replace(PATH_DIV_REPLACE, PATH_DIV)
        while path.contains(PATH_DIV_TWICE) {
            path = path.replace(PATH_DIV_TWICE, PATH_DIV)
        }
        if path.ends_with(PATH_DIV) : path = path.rtrim(PATH_DIV)
        if path.empty() : path = PATH_DIV
        return SELF { path: path }
    }

    + fn add(part: String) SELF {
        part = part.replace(PATH_DIV_REPLACE, PATH_DIV).trim(PATH_DIV)
        while part.contains(PATH_DIV_TWICE) {
            part = part.replace(PATH_DIV_TWICE, PATH_DIV)
        }
        if part.empty() : return this
        let path = this.path + PATH_DIV + part
        return SELF { path: path }
    }

    + fn basename() String {
        return basename(this.path)
    }

    + fn pop() SELF {
        let path = dir_of(this.path)
        if path.length > 1 && path.ends_with(PATH_DIV) {
            path = path.substr(0, path.bytes - 1)
        }
        return SELF { path: path }
    }

    + fn str() String $to {
        return this.path
    }
}

+ fn cwd() String {
    let buf : cstring = @stack([4096 x u8])
    let dir = sys:getcwd(buf, 4096)
    if !isset(dir) : panic("cwd: OS failed to return the current directory")
    return dir
}

+ fn chdir(path: String) {
    sys:chdir(path.data_ptr())
}

+ fn exe_dir() String {
    let dir = EXE_DIR
    if isset(dir) : return dir
    let d = dir_of(exe_path())
    EXE_DIR = d
    return d
}

+ fn exe_path() String {
    let p = EXE_PATH
    if isset(p) {
        return p
    }
    // TODO: USE PATH_MAX
    let buf : ptr[4096 x u8] = @stack([4096 x u8])

    #if OS == win
    let len = sys:GetModuleFileNameA(null, buf, PATH_MAX)
    let str = String.make_from_ptr(buf, len)
       
    #elif OS == macos

    let size : u32 = PATH_MAX;
    if sys:_NSGetExecutablePath(buf, @ref(size)) < 0 {
        println("Could not determine macos valk executable path\n");
        exit(1);
    }
    let path : String = buf.@cast(cstring)
    return realpath(path)

    #else
    let link = "/proc/self/exe".data_cstring()
    let len = sys:readlink(link, buf, PATH_MAX)
    let str = String.make_from_ptr(buf, len.to(u64))
    #end

    EXE_PATH = str
    return str
}

+ fn realpath(path: String) String {
    let buf : ptr[4096 x u8] = @stack([4096 x u8])
    #if OS == win
        return sys:_fullpath(buf, path.data_ptr(), PATH_MAX);
    #else
    path = path.replace(PATH_DIV_REPLACE, PATH_DIV)
    if exists(path) {
        return sys:realpath(path.data_ptr(), buf)
    }
    return path
    #end
}

+ fn ext(path: String, with_dot: bool (false)) String {
    let len = path.bytes
    let start = len
    let found = false
    while start > 0 {
        start--
        let ch = path.get(start)
        if ch == '.' {
            if with_dot == false : start++
            found = true
            break
        }
        if ch == '/' || ch == '\\' : break
    }
    if found == false : return ""
    return path.substr(start, len - start)
}

// e.g. /etc/nginx/nginx.conf -> /etc/nginx
// e.g. /etc/nginx/ -> /etc
// e.g. /etc -> /
// e.g. "" -> ""
// e.g. etc -> etc
+ fn dir_of(path: String) String {
    let bytes = path.bytes
    if bytes == 0 : return ""
    let end = bytes - 1
    while end-- > 0 {
        let ch = path.get(end)
        if ch == '/' || ch == '\\' {
            if end == 0 : end++
            return path.substr(0, end)
        }
    }
    return path
}

// e.g. /etc/nginx/nginx.conf -> nginx.conf
// e.g. /etc/nginx/ -> nginx
// e.g. / -> ""
// e.g. "" -> ""
+ fn basename(path: String) String {
    let len = path.bytes
    let start = len
    let rtrim : uint = 0
    while start > 0 {
        let ch = path.get(start - 1)
        if ch == '/' || ch == '\\' {
            start--
            rtrim++
            continue
        }
        break
    }
    while start-- > 0 {
        let ch = path.get(start)
        if(ch == '/' || ch == '\\'){
            start++
            break
        }
    }
    return path.substr(start, len - start - rtrim)
}

+ fn home_dir() String !not_found {
    #if OS == win
    return core:getenv("USERPROFILE") ! throw not_found
    #end
    return core:getenv("HOME") ! throw not_found
}
