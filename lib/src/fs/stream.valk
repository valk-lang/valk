
use io

+ fn stream(path: String, read: bool, write: bool, append: bool (false), auto_create: bool (false)) FileStream !err_open {
    let fd = open_extend(path, write, append, auto_create) ! throw err_open
    return FileStream{ path: path, fd: fd, can_read: read, can_write: write }
}

+ class FileStream {
    ~ path: String
    - fd: FD 
    - can_read: bool
    - can_write: bool
    ~ reading: bool (true)

    + fn read(bytes: uint (10240)) String !read_err {
        if !this.can_read || !this.reading : return ""

        // TODO : use byte buffer
        let str = String.make_empty(bytes)
        let rcount = io:read(this.fd, str.data_ptr(), bytes) ! throw read_err
        @ptrv(@ref(str.bytes), uint) = rcount
        @ptrv(str.data_ptr(), u8, rcount) = 0

        if rcount < bytes : this.close()

        return str
    }

    + fn write_bytes(from: ptr, len: uint) !write_err {
        let rcount : uint = 0
        while len - rcount > 0 {
            rcount += io:write_bytes(this.fd, from + rcount, len - rcount) ! throw write_err
        }
    }

    + fn close() {
        if this.reading {
            this.reading = false
            io:close(this.fd)
        }
    }

    fn gc_free() {
        this.close()
    }
}
