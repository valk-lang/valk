
header "sys" as sys

use valk:gc
use valk:coro
use valk:mem

global current_thread: ?Thread (null)

class Thread {
    - thread: ?ptr (null)
    - handler: fn()()
    ~ finished: bool (false)

    static fn start(func: imut fn()()) SELF {
        let t = SELF {
            handler: func @as fn()()
        }
        t.init()
        return t
    }
    static fn start_unsafe(func: fn()()) SELF {
        let t = SELF {
            handler: func
        }
        t.init()
        return t
    }

    fn init() {

        gc:share(this)

        #if OS == win
        this.thread = sys:CreateThread(null, 0, this.entryx, this, 0, null)
        #else
        let thr = sys:pthread_t{ data: 0 }
        let err = sys:pthread_create(thr, null, this.entryx, this)
        // if err != 0 {
        // 	throw fail
        // }
        this.thread = thr
        #end
    }

    fn entryx() $entrance {

        // Init
        gc:thread_init()
        gc:GcLocal.init()

        @allocas

        let gc = gc:local_gc

        #loop globals G T
        #if !global_is_shared(G)
        G = @global_default_value(G)
        #if is_gc_type(T)
        gc.add_global(@ref(G))
        #end
        #end
        #end

        current_thread = this

        gc.push_to_shared()

        // Run
        let run_until = (co this.handler()) @as coro:Coro
        coro:Coro.loop(run_until)

        // Stop
        this.finished = true
        gc:shutdown()
    }

    fn wait() {
        let thr = this.thread
        if isset(thr) {
            #if OS == win
            sys:WaitForSingleObject(thr, 0)
            #else
            sys:pthread_join((thr @as sys:pthread_t).data, null)
            #end
        }
    }

    fn gc_free() {
        let thr = this.thread
        if isset(thr) {
            #if OS == win
            sys:CloseHandle(thr)
            #else
            mem:free(thr)
            #end
        }
    }
}

// // Example
// let x = 5
// Thread.start(imut fn() bool {
//     println("Hello from other thread: " + x)
//     return true
// })
