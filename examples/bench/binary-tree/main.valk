
use valk:time
use valk:gc

class Node {
    left: ?Node (null)
    right: ?Node (null)

    static fn tree(x: int) Node $hot {
        if x == 0 {
            let res = Node {}
            return res
        }
        return Node {
            left: Node.tree(x - 1)
            right: Node.tree(x - 1)
        }
    }

    fn check() uint $hot {
        let res : uint = 1
        let lt = this.left
        let rt = this.right
        if isset(lt) && isset(rt) {
            res += lt.check() + rt.check()
        }
        return res
    }
}

fn stretch(depth: int) {
    let check = Node.tree(depth).check()
    println("stretch tree of depth " + depth + "\t check: " + check)
}

value min_depth (4)

fn main(args: Array[String]) {

    let max_depth = 5
    args.get(1) -> str : max_depth = str.to_int() ! panic("Invalid number: " + str)

	if min_depth + 2 > max_depth : max_depth = min_depth + 2

    let start = time:microtime()

    stretch(max_depth + 1)

    let longlived = Node.tree(max_depth)

    let x = min_depth
    while x <= max_depth {
        let mul = (max_depth - x + min_depth)
        let iters = 1
        while mul-- > 0 {
            iters = iters * 2
        }
        let check = 0.to(uint)

        let i = 0
        while i++ < iters {
            let t = Node.tree(x)
            check += t.check()
        }
        println(iters + "\t trees of depth " + x + "\t check: " + check)
        x += 2
    }

    let ll = longlived.check()
    println("long lived tree of depth " + max_depth + "\t check: " + ll)

    println("# Time required: " + ((time:microtime() - start) / 1000) + " ms")
    println("# GC peak memory usage: " + (gc:mem_usage_peak / 1024) + " KB")
}
